// DamageType that prevents players from damaging one another in coop rounds
DamageType CoopBullet
{
	// Doesn't need anything
}

// WAIT BUU! YOU'RE NOT SUPPOSED TO DO THIS!
// It's necessary. Only way to correct a bug with telefragging that's been haunting this gamemode since its inception
ACTOR DoomPlayer : PlayerPawn
{
	Speed 1
	Health 100
	Radius 16
	Height 56
	Mass 100
	PainChance 255
	DamageFactor "CoopBullet", 0
	Player.DisplayName "Marine"
	Player.CrouchSprite "PLYC"
	Player.StartItem "Pistol"
	Player.StartItem "Fist"
	Player.StartItem "Clip", 50
	Player.WeaponSlot 1, Fist, Chainsaw
	Player.WeaponSlot 2, Pistol
	Player.WeaponSlot 3, Shotgun, SuperShotgun
	Player.WeaponSlot 4, Chaingun
	Player.WeaponSlot 5, RocketLauncher
	Player.WeaponSlot 6, PlasmaRifle
	Player.WeaponSlot 7, BFG9000
	Player.ColorRange 112, 127
	Player.ColorSet 0, "Green",         0x70, 0x7F,  0x72
	Player.ColorSet 1, "Gray",          0x60, 0x6F,  0x62 // Called "Indigo" originally so as to have a unique initial
	Player.ColorSet 2, "Brown",         0x40, 0x4F,  0x42
	Player.ColorSet 3, "Red",           0x20, 0x2F,  0x22
	// Doom Legacy additions
	Player.ColorSet 4, "Light Gray",    0x58, 0x67,  0x5A
	Player.ColorSet 5, "Light Brown",   0x38, 0x47,  0x3A
	Player.ColorSet 6, "Light Red",     0xB0, 0xBF,  0xB2
	Player.ColorSet 7, "Light Blue",    0xC0, 0xCF,  0xC2
	+NOTELESTOMP
	-TELESTOMP
	+NOTELEFRAG
	-ALWAYSTELEFRAG
	States
	{
		Spawn:
			PLAY A -1
			Loop
		See:
			PLAY ABCD 4 
			Loop
		Missile:
			PLAY E 12
			Goto Spawn
		Melee:
			PLAY F 6 BRIGHT
			Goto Missile
		Pain:
			PLAY G 4 
			PLAY G 4 A_Pain
			Goto Spawn
		Death:
			PLAY H 0 A_PlayerSkinCheck("AltSkinDeath")
		Death1:
			PLAY H 10
			PLAY I 10 A_PlayerScream
			PLAY J 10 A_NoBlocking
			PLAY KLM 10
			PLAY N -1
			Stop
		XDeath:
			PLAY O 0 A_PlayerSkinCheck("AltSkinXDeath")
		XDeath1:
			PLAY O 5
			PLAY P 5 A_XScream
			PLAY Q 5 A_NoBlocking
			PLAY RSTUV 5
			PLAY W -1
			Stop
		AltSkinDeath:
			PLAY H 6
			PLAY I 6 A_PlayerScream
			PLAY JK 6
			PLAY L 6 A_NoBlocking
			PLAY MNO 6
			PLAY P -1
			Stop
		AltSkinXDeath:
			PLAY Q 5 A_PlayerScream
			PLAY R 0 A_NoBlocking
			PLAY R 5 A_SkullPop
			PLAY STUVWX 5
			PLAY Y -1
			Stop
	}
}

// Floating skull replacement for bobbing
ACTOR FloatingSkull2 : FloatingSkull replaces FloatingSkull
{
	+NOGRAVITY
	+FloatBOB
}

// Rocket launcher replacement (with team support)
ACTOR RocketLauncher2 : RocketLauncher replaces RocketLauncher
{
	States
	{
		Fire:
			MISG B 8 A_GunFlash
			MISG B 0 A_JumpIfInventory("BlueTeamItem", 1, "FireBlue")
			MISG B 0 A_JumpIfInventory("RedTeamItem", 1, "FireRed")
			MISG B 12 A_FireMissile
			MISG B 0 A_ReFire
			Goto Ready
		FireBlue:
			MISG B 12 A_FireCustomMissile("Rocket2Blue")
			MISG B 0 A_ReFire
			Goto Ready
		FireRed:
			MISG B 12 A_FireCustomMissile("Rocket2Red")
			MISG B 0 A_ReFire
			Goto Ready
	}
}

// Rocket replacement so that they spawn with TID_REMOVE
ACTOR Rocket2 : Rocket replaces Rocket
{
	States
	{
		Spawn:
			MISL A 0 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			MISL A 1 Bright
			MISL A 0 ACS_NamedExecuteAlways("DoomWare_Entity_CheckCleanup", 0)
			Loop
	}
}

// Doomware Grenade (Blue Team)
ACTOR Rocket2Blue : Rocket2
{
	Translation "128:143=199:207", "123:127=240:241", "144:151=199:207", "1:1=242:242", "64:79=240:243", "152:159=207:207"
}

// Doomware Grenade (Red Team)
ACTOR Rocket2Red : Rocket2
{
	Translation "128:143=179:187", "123:127=190:191", "144:151=176:191", "1:1=191:191", "64:79=176:191", "152:159=190:190"
}

// Skulltag railgun
ACTOR RailGun : DoomWeapon 5012
{
	Game Doom
	SpawnID 164
	Radius 20
	Height 16
	Weapon.Selectionorder 100
	Weapon.AmmoUse 10
	Weapon.AmmoGive 40
	Weapon.AmmoType "Cell"
	Inventory.Pickupmessage "$PICKUP_RAILGUN"
	Obituary "%o was penetrated deep by %k"
	States
	{
		Ready:
			RLGG A 1 A_WeaponReady
			loop
		Deselect:
			RLGG A 1 A_Lower
			loop
		Select:
			RLGG A 1 A_Raise
			loop
		Fire:
			RLGG B 12 A_RailAttack(100, 0, true, "", "", 0, 0, "CoopBulletPuff")
			RLGG A 6
			Goto Ready
		Flash:
			TNT1 A 5 bright A_Light1
			TNT1 A 5 bright A_Light2
			TNT1 A 0 bright A_Light0
			Goto LightDone
	}
}

// Ralphis head (Terry level)
ACTOR dickfail
{
	health 700
	mass 1
	scale 3.0
	height 120
	painchance 256
	deathsound "DoomWare/Spam"
	seesound "DoomWare/Spam"
	activesound "DoomWare/Yaya"
	obituary "%o, can I fuck your ass? Yes!"
	speed 8
	MONSTER
	+SOLID
	+BOSS
	+NOTARGET
	+SHOOTABLE
	+NOICEDEATH
	+LOOKALLAROUND
	States
	{
		Spawn:
			MYYO A 1 a_look
			MYYO A 0 A_AlertMonsters(0)
			loop
		See:
			MYYO A 1 A_Chase
			loop
		Missile:
			MYYO A 10 A_CustomMissile("DickShot", 48, 0)
			goto See
		Pain:
			MYYO A 1 A_Pain
			goto See
		Death:
			MYYO A 1
			stop
	}
}

// Ralphis head (Flight Wacky Mod)
ACTOR dickfail_Flight : dickfail
{
	+FLOAT
	+NOGRAVITY
}

// Ralphis head (Rage Wacky Mod)
ACTOR dickfail_Rage : dickfail
{
	States
	{
		Missile:
			MYYO A 5 A_CustomMissile("DickShot", 48, 0)
			goto See
	}
}

// Ralphis head (Spread Wacky Mod)
ACTOR dickfail_Spread : dickfail
{
	States
	{
		Missile:
			MYYO A 0 A_CustomMissile("DickShot", 48, 0, -10)
			MYYO A 0 A_CustomMissile("DickShot", 48, 0, 10)
			MYYO A 10 A_CustomMissile("DickShot", 48, 0)
			goto See
	}
}

// Ralphis head shot
ACTOR DickShot : FatShot
{
	Damage 4
}

// Ghoul's Forest Jitterskull
ACTOR Jitterskull
{
	Scale 0.13
	Height 70
	Radius 15
	Speed 23
	Damage 255
	Health 800
	MONSTER
	+NOBLOCKMONST
	+DROPOFF
	+LOOKALLAROUND
	+QUICKTORETALIATE
	Obituary "%o was spooked by the jitterskull"
	painchance 0
	mass 9999999999
	States
	{
		Spawn:
			JSKL A 1 A_Look
			Loop
		See:
			JSKL A 0 A_Stop
			JSKL A 0 A_PlaySound("DoomWare/JitterskullCackle1")
			JSKL D 0 A_Chase
			JSKL ABCBABCBCBABABCBCBABABCBCBA 1 A_FaceTarget
			JSKL A 0 A_JumpIfCloser(280, 21)
			JSKL AAAAAAAAAAAAAAAAAAAA 0 A_Chase
		Loop
			JSKL D 0 A_FaceTarget
			JSKL D 0 A_PlaySoundEx("DoomWare/JitterskullAttack", "Voice",0)
			Goto Pain
		Pain:
			JSKL D 0 A_FaceTarget
			JSKL D 1 A_SkullAttack
			JSKL D 0 A_JumpIfCloser(280, 2)
			JSKL D 1 A_Stop
		Goto See
			JSKL D 0
			Loop
		Death:
			Stop
	}
}

// Melee weapon bullet puff
ACTOR MeleePuff : BulletPuff
{
	States
	{
		Spawn:
			PUFF CD 4
			Stop
	}
}

// Hexen mace for barrel/diglett minigame
ACTOR MaceWeapon : DoomWeapon
{
	Weapon.SelectionOrder 3500
	Weapon.KickBack 150
	Weapon.YAdjust -8
	Obituary "%o was whacked by %k"
	+BLOODSPLATTER
	+NOAUTOAIM
	States
	{
		Select:
			CMCE A 1 A_Raise
			Loop
		Deselect:
			CMCE A 1 A_Lower
			Loop
		Ready:
			CMCE A 1 A_WeaponReady
			Loop
		Fire:
			CMCE B 2 Offset(60, 20)
			CMCE B 1 Offset(30, 33) A_PlaySound("DoomWare/MaceSwing")
			CMCE B 2 Offset(8, 45)
			CMCE C 1 Offset(8, 45)
			CMCE D 1 Offset(8, 45)
			CMCE E 1 Offset(8, 45)
			CMCE E 1 Offset(-11, 58) A_FireBullets(0, 0, 1, 20, "MeleePuff", FBF_NOFLASH | FBF_NORANDOM, 64) // A_CustomPunch(20, true, CPF_NOTURN) // CPF_NOTURN Flag unsupported in Zandronum :/
			CMCE F 1 Offset(8, 45)
			CMCE F 2 Offset(-8, 74)
			CMCE F 1 Offset(-20, 96)
			CMCE F 8 Offset(-33, 160)
			CMCE A 2 Offset(8, 75) A_ReFire
			CMCE A 1 Offset(8, 65)
			CMCE A 2 Offset(8, 60)
			CMCE A 1 Offset(8, 55)
			CMCE A 2 Offset(8, 50)
			CMCE A 1 Offset(8, 45)
			Goto Ready
	}
}

// Homer Simpson shotgun for Ned Flanders level
ACTOR SimpsonsShotgun : Shotgun
{
	States
	{
		Ready:
			SMPS A 1 A_WeaponReady
			Loop
		Deselect:
			SMPS A 1 A_Lower
			Loop
		Select:
			SMPS A 1 A_Raise
			Loop
		Fire:
			SMPS A 3
			SMPS A 7 A_FireShotgun
			SMPS BC 5
			SMPS D 4
			SMPS CB 5
			SMPS A 3
			SMPS A 7 A_ReFire
			Goto Ready
		Flash:
			SHTF A 4 Bright A_Light1
			SHTF B 3 Bright A_Light2
			Goto LightDone
		Spawn:
			SHOT A -1
			Stop
	}
}

// Ned Flanders himself
ACTOR NedFlanders : DoomImp
{
	Health 60
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	SeeSound "DoomWare/FlandersSpot"
	PainSound "DoomWare/FlandersPain"
	DeathSound "DoomWare/FlandersDeath"
	ActiveSound "DoomWare/FlandersAct"
	HitObituary "$OB_IMPHIT"
	Obituary "%o was mur-diddly-urdlered"
	States
	{
		Spawn:
			NEDF AB 10 A_Look
			Loop
		See:
			NEDF AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			NEDF EF 8 A_FaceTarget
			NEDF G 6 A_CustomComboAttack("FlandersBall", 32, 3 * random(1, 8), "imp/melee")
			Goto See
		Pain:
			NEDF H 2
			NEDF H 2 A_Pain
			Goto See
		XDeath:
		Death:
			NEDF I 8 A_GiveToTarget("WonRoundItem",1)
			NEDF J 8 A_Scream
			NEDF K 6
			NEDF L 6 A_NoBlocking
			NEDF M -1
			Stop
	}
}

// Ned Flanders (Flight Wacky Mod)
ACTOR NedFlanders_Flight : NedFlanders
{
	+FLOAT
	+NOGRAVITY
}

// Ned Flanders (Rage Wacky Mod)
ACTOR NedFlanders_Rage : NedFlanders
{
	States
	{
		Melee:
		Missile:
			NEDF EF 4 A_FaceTarget
			NEDF G 3 A_CustomComboAttack("FlandersBall", 32, 3 * random(1, 8), "imp/melee")
			Goto See
	}
}

// Ned Flanders (Spread Wacky Mod)
ACTOR NedFlanders_Spread : NedFlanders
{
	States
	{
		Melee:
		Missile:
			NEDF EF 8 A_FaceTarget
			NEDF G 0 A_CustomMissile("DoomImpBall", 32, 0, -10)
			NEDF G 0 A_CustomMissile("DoomImpBall", 32, 0, 10)
			NEDF G 6 A_CustomComboAttack("FlandersBall", 32, 3 * random(1, 8), "imp/melee")
			Goto See
	}
}

// Ned Flander's Fireball
ACTOR FlandersBall : DoomImpBall
{
	States
	{
		Spawn:
			FBAL AB 4 Bright
			Loop
		Death:
			FBAL CDE 6 Bright
			Stop
	}
}

// Item so the game knows the player won the round
ACTOR WonRoundItem : Ammo 10469
{
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 10000
	Inventory.PickupSound "DoomWare/Ring"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10000
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0 A_playsound("DoomWare/Ring")
			stop
	}
}

// Item so the game knows the player failed the round
ACTOR FailRoundItem : Ammo 10470
{
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 10000
	Inventory.PickupSound "DoomWare/Ring"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10000
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0 A_playsound("DoomWare/Ring")
			stop
	}
}

// Blue Team item
ACTOR BlueTeamItem : Ammo
{
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0
			stop
	}
}

// Red Team item
ACTOR RedTeamItem : Ammo
{
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0
			stop
	}
}

// Unused Mario64 coin item. Might use in the future
ACTOR CoinItem : Ammo 10471
{
	Scale 0.5
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 10000
	Inventory.PickupSound "DoomWare/Coin"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10000
	states
	{
		Spawn:
			COIN ABCD 4
			Loop
		Death:
			COIN A 0 A_playsound("DoomWare/Coin")
			stop
	}
}

// Barrel used in barrel minigame
ACTOR WoodBarrel : DoomImp
{
	Health 40
	Radius 10
	Height 42
	Mass 100
	Speed 0
	PainChance 0
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	Monster
	+FLOORCLIP
	+NoBlood
	States
	{
		Spawn:
			BARL A 10 A_Look
			Loop
		See:
			BARL A 3
			Loop
		Death:
			BARL A 0 A_GiveToTarget("WonRoundItem",1)
			BARL B 0 A_PlaySound("DoomWare/Barrel")
			BARL BBBBBBBBBBBBBBBBBBBBBBB 0 A_CustomMissile("BarrelGib1",16,0,random(0,360),2,random(-20,90))
			BARL BBBB 0 A_CustomMissile("BarrelGib2",16,0,random(0,360),2,random(-20,90))
			BARL BB 0 A_CustomMissile("BarrelGib3",16,0,random(0,360),2,random(-20,90))
			BARL B 1
			BARL B -1
			Stop
		XDeath:
			goto Death
	}
}

// Green 1Up barrel
ACTOR WoodBarrel1UP : WoodBarrel
{
	translation "64:79=112:123", "236:239=120:124", "1:2=125:126", "144:151=112:122"
	States
	{
		Spawn:
			BARL L 10 A_Look
			Loop
		See:
			BARL L 3
			Loop
		Death:
			BARL L 0 A_GiveToTarget("1up",1)
			BARL B 0 A_PlaySound("DoomWare/Barrel")
			BARL BBBBBBBBBBBBBBBBBBBBBBB 0 A_CustomMissile("BarrelGib11UP",16,0,random(0,360),2,random(-20,90))
			BARL BBBB 0 A_CustomMissile("BarrelGib21UP",16,0,random(0,360),2,random(-20,90))
			BARL BB 0 A_CustomMissile("BarrelGib3",16,0,random(0,360),2,random(-20,90))
			BARL B 1
			BARL A 0 A_GiveToTarget("WonRoundItem",100)
			BARL B -1
			Stop
	}
}

// Barrel gib 1
ACTOR BarrelGib1
{
	PROJECTILE
	Damage 0
	Height 2
	radius 2
	- NOGRAVITY
	Speed 10
	States
	{
		Spawn:
			BARL FFGGHHGGFFGGHHGGFFGGHHGGFFGGHHGG 1
			Stop
		Death:
			MARB A 1
			Stop
	}
}

// Barrel gib 2
ACTOR BarrelGib2
{
	PROJECTILE
	Damage 0
	Height 2
	radius 2
	- NOGRAVITY
	Speed 10
	States
	{
		Spawn:
			BARL IIJJKKJJIIJJKKJJIIJJKKJJIIJJKKJJ 1
			Stop
		Death:
			MARB A 1
			Stop
	}
}

// Barrel gib 3
ACTOR BarrelGib3
{
	PROJECTILE
	Damage 0
	Height 2
	radius 2
	- NOGRAVITY
	Speed 10
	States
	{
		Spawn:
			BARL CCDDEEDDCCDDEEDDCCDDEEDDCCDDEEDD 1
			Stop
		Death:
			MARB A 1
			Stop
	}
}

// Green barrel gib 1
ACTOR BarrelGib11UP : BarrelGib1
{
	translation "64:79=112:123", "236:239=120:124", "1:2=125:126", "144:151=112:122"
}

// Green barrel gib 2
ACTOR BarrelGib21UP : BarrelGib2
{
	translation "64:79=112:123", "236:239=120:124", "1:2=125:126", "144:151=112:122"
}

// Beheaded Kamikaze
ACTOR Jihad
{
	Health 10
	PainChance 180
	Speed 20
	Radius 16
	Height 56
	Mass 100
	Monster
	+FLOORCLIP
	SeeSound "doomWare/Jihad"
	Obituary "%o was Kamikazi'd."
	States
	{
		Spawn:
			BKAM AB 10 A_Look
			Loop
		See:
			BKAM AABBCCDD 2 A_Chase
			Loop
		Death:
			BMBE M 0 A_PlaySound("DoomWare/JihadBoom")
			BMBE M 8 A_Explode(512,256)
			BMBE N 8
			BMBE O 8
			BMBE P 8
			BMBE Q 4
			BMBE R 4 A_NoBlocking
			BMBE S 4
			BMBE T 4
			BMBE U 4
			BMBE U -1
			Stop
	}
}

ACTOR Jihad_Spread : Jihad
{
	// Exactly the same
}

ACTOR Jihad_Rage : Jihad
{
	// Exactly the same
}

ACTOR Jihad_Flight : Jihad
{
	+FLOAT
	+NOGRAVITY
}

// Suicide pistol
ACTOR Suicide : DoomWeapon
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Obituary "%o became bored with life."
	States
	{
		Ready:
			SUIC A 1 A_WeaponReady
			Loop
		Deselect:
			SUIC A 1 A_Lower
			Loop
		Select:
			SUIC A 1 A_Raise
			Loop
		Fire:
			SUIC A 4
			SUIC B 6
			SUIC B 0
			SUIC C 4 A_PlaySound("weapons/pistol", CHAN_WEAPON)
			SUIC C 0 A_DIE
			SUIC B 5 A_ReFire
			Goto Ready
		Flash:
			SUIC A 7 Bright A_Light1
			Goto LightDone
			SUIC A 7 Bright A_Light1
			Goto LightDone
		Spawn:
			PIST A -1
			Stop
	}
}

// Massmouth Raygun
ACTOR Raygun : DoomWeapon
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
	Weapon.AmmoType "Clip"
	States
	{
		Ready:
			MMPS A 1 A_WeaponReady
			Loop
		Deselect:
			MMPS A 1 A_Lower
			Loop
		Select:
			MMPS A 1 A_Raise
			Loop
		Fire:
			Goto Ready
		Spawn:
			PIST A -1
			Stop
	}
}

// Massmouth Worm
ACTOR Worm 32767
{
	Radius 16
	height 42
	ProjectilePassHeight -16
	+ SOLID
	States
	{
		Spawn:
			WORM AB 6
			loop
	}
}

// Sonic player morph
ACTOR SonicPlayer : DoomPlayer
{
	+NOSKIN
	-NOTELESTOMP
	+TELESTOMP
	-NOTELEFRAG
	-ALWAYSTELEFRAG
	Player.ColorRange 112, 127
	Player.SoundClass "sonic"
	States
	{
		Spawn:
			SONC A -1
			Loop
		See:
			SONC ABCD 4
			Loop
	}
}

// Buu342 player morph
ACTOR Buu342Player : DoomPlayer
{
	scale 0.22
	+PICKUP
	+NOSKIN
	Player.SoundClass "buu342"
	States
	{
		Spawn:
			BUDW A -1
			Loop
		See:
			BUDW ABCD 4 
			Loop
		Missile:
			BUDW E 12
			Goto Spawn
		Melee:
			BUDW F 6 BRIGHT
			Goto Missile
		Pain:
			BUDW G 4 
			BUDW G 4 A_Pain
			Goto Spawn
		Death:
			BUDW I 10
			BUDW J 10 A_PlayerScream
			BUDW K 10 A_NoBlocking
			BUDW L 10
			BUDW M -1
			Stop
		XDeath:
			BUDW N 5
			BUDW O 5 A_XScream
			BUDW P 5 A_NoBlocking
			BUDW Q 5
			BUDW R -1
			Stop
	}
}

// Chicken player morph
ACTOR ChickenPlayer2 : ChickenPlayer
{
    +NOSKIN
	Player.JumpZ 0
	Player.ForwardMove 0.75, 0.5
	Player.SideMove 0.75, 0.5
	Player.MorphWeapon "Beak2"
	Player.SoundClass "Chicken"
	DeathSound "DoomWare/ChickenDeath"
	States
	{
		Missile:
			Goto Spawn
		Pain:
			CHKN B 4 A_Feathers
			Goto Spawn
	}
}

// Chicken player morph, but with sonic speed
ACTOR ChickenPlayer2Speed : ChickenPlayer2
{
	Player.ForwardMove 1.5, 1
	Player.SideMove 1.5, 1
	States
	{
		See:
			CHKN A 3 A_Feathers
			CHKN B 3 A_Feathers
			Loop
	}
}

// Chicken beak weapon
ACTOR Beak2 : Beak
{
	States
	{
		Fire:
			Goto Ready
	}
}

// Massmouth player morph
ACTOR MassMouthPlayer : DoomPlayer
{
	+NOSKIN
	Player.SoundClass "massm"
	States
	{
		Spawn:
			MASS A -1
			Loop
		See:
			MASS ABCD 4
			Loop
		Missile:
			Goto Spawn
	}
}

// Simpsons player morph
ACTOR SimpsonsPlayer : DoomPlayer
{
	Health 100
	+NOSKIN
	+NODAMAGE
	Player.SoundClass "homer"
	States
	{
	}
}

// Kart player morph
ACTOR KartPlayer : DoomPlayer
{
	Player.ForwardMove 0
	Player.SideMove 0
	player.viewheight 24
	Player.JumpZ 0
	Health 100
	Radius 16
	Height 30
	Mass 100
	Player.ColorRange 112, 127
	PainSound ""
	DeathSound ""
	Player.MorphWeapon "KartWeapon"
	Player.SoundClass "Kart"
	+NOBLOOD
	+pushable
	+NOSKIN
	States
	{
		Missile:
			Goto See
		See:
			Spawn:
			KART A 5
			Loop
		Death:
			MISL B 5 bright A_Playsound("weapons/rocklx")
			MISL C 5 bright
			MISL D 5 bright
			TNT2 A 5 bright
			TNT2 A -1
			Stop
		XDeath:
			MISL B 5 bright A_Playsound("weapons/rocklx")
			MISL C 5 bright
			MISL D 5 bright
			TNT2 A 5 bright
			TNT2 A -1
			Stop
	}
}

// Kart wheel
ACTOR KartWeapon : DoomWeapon
{
	Weapon.SelectionOrder 2000
	+WEAPON.DONTBOB
	+WEAPON.ALT_AMMO_OPTIONAL
	Weapon.KickBack 20000
	Weapon.AmmoUse2   0
	States
	{
		Ready:
			KRTW A 1 A_WeaponReady
			KRTW A 0 A_StopSound(6)
			loop
		Deselect:
			KRTW A 0 A_StopSound(6)
			KRTW A 1 A_Lower
			loop
		Select:
			KRTW A 1 A_Raise
			Loop
		Fire:
			KRTW A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "FireRage")
			KRTW A 2 A_Recoil(-4)
			KRTW A 0 A_PlaySound("DoomWare/KartEngine", 6, 1)
			KRTW A 0 A_ReFire
			goto Ready
		FireRage:
			KRTW A 4 A_Recoil(-4)
			KRTW A 0 A_PlaySound("DoomWare/KartEngine", 6, 1)
			KRTW A 0 A_ReFire
			goto Ready
	}
}

// Kart player morph (Sonic Speed)
ACTOR KartPlayerSpeed : KartPlayer
{
	Player.MorphWeapon "KartWeaponFast"
}

// Kart wheel (Sonic Speed)
ACTOR KartWeaponFast : KartWeapon
{
	States
	{
		Fire:
			KRTW A 0 A_JumpIfInventory("PowerDoubleFiringSpeed", 1, "FireRage")
			KRTW A 2 A_Recoil(-8)
			KRTW A 0 A_PlaySound("DoomWare/KartEngine", 6, 1)
			KRTW A 0 A_ReFire
			goto Ready
		FireRage:
			KRTW A 4 A_Recoil(-8)
			KRTW A 0 A_PlaySound("DoomWare/KartEngine", 6, 1)
			KRTW A 0 A_ReFire
			goto Ready
	}
}

// Player morph color translation helper class
ACTOR PlayerTranslator
{
	States
	{
		Spawn:
			TNT1 N 1
			Goto Execute
		Execute:
			TNT1 N 1
			TNT1 N 0 ACS_NamedExecuteAlways("DoomWare_Entity_SetPlayerTranslation",0)
			TNT1 N 1
			Goto Execute
	}
}

// Mario Kart Lakitu particle
ACTOR Lakitu
{
	+NOGRAVITY
	+FloatBOB
	States
	{
		Spawn:
			LAKI BC 35 BRIGHT
			Goto Death
		Death:
			LAKI D 0 ThrustThingZ(0,16,0,0)
			Laki D 35 BRIGHT
			TNT1 D 0
			Stop
	}
}

// Spiky pole
ACTOR SpikeTube 11111
{
	Game Doom
	radius 16
	height 128
	Mass 0x7fffffff
	Obituary "%o ran into a rotating spike pole."
	-SOLID
	-SHOOTABLE
	+EXTREMEDEATH
	+QUICKTORETALIATE
	+NOTARGET
	+CANTSEEK
	+PIERCEARMOR
	+OLDRADIUSDMG
	+LOOKALLAROUND
	+NOTIMEFREEZE
	+NOICEDEATH
	+NOGRAVITY
	+NODAMAGE
	+NOBLOOD
	+Noclip
	States
	{
		Spawn:
			SPIK ABCDEFGH 2 A_Look
			Goto See
		See:
			SPIK ABCDEFGH 2 A_VileChase
			Goto Melee
		Melee:
			SPIK ABCDEFGH 2 A_Explode(100,32,1,1,48,0,0)
			Goto See
	}
}

// Spiky pole, but with a bigger radius
ACTOR SpikeTube2 : SpikeTube 11100
{
	radius 128
	States
	{
		Melee:
			SPIK ABCDEFGH 2 A_Explode(100,128,1,1,48,0,0)
			Goto See
	}
}

// Spiky pole, but with a thinner radius
ACTOR SpikeTube3 : SpikeTube 11101
{
	radius 4
	States
	{
		Melee:
			SPIK ABCDEFGH 2 A_Explode(100,8,1,1,48,0,0)
			Goto See
	}
}

// Sonic the Hedgehog green sunflower prop
ACTOR SonicFlower1 11112
{
	Radius 16
	height 64
	- SOLID
	States
	{
		Spawn:
			SCFW AB 16
			Loop
	}
}

// Sonic the Hedgehog purple sunflower prop
ACTOR SonicFlower2 11113
{
	Radius 16
	height 64
	- SOLID
	States
	{
		Spawn:
			SCFW CD 16
			Loop
	}
}

// Sonic the Hedgehog cyan sunflower prop
ACTOR SonicFlower3 11114
{
	Radius 16
	height 64
	- SOLID
	States
	{
		Spawn:
			SCFW EF 16
			Loop
	}
}

// Sonic the Hedgehog cyan flower prop
ACTOR SonicFlower4 11115
{
	Radius 16
	height 54
	- SOLID
	States
	{
		Spawn:
			SCFW GHIH 16
			Loop
	}
}

// Sonic the Hedgehog pole with wings prop
ACTOR SonicPole1 11116
{
	Radius 24
	height 48
	+ SOLID
	States
	{
		Spawn:
			SCPL A 1
			Loop
	}
}

// Sonic the Hedgehog pole without wings prop
ACTOR SonicPole2 11117
{
	Radius 8
	height 48
	+ SOLID
	States
	{
		Spawn:
			SCPL B 1
			Loop
	}
}

// Sonic the Hedgehog rock prop
ACTOR SonicRock 11118
{
	Radius 24
	height 32
	+ SOLID
	States
	{
		Spawn:
			SCRK A 1
			Loop
	}
}

// Sonic the Hedgehog bush prop
ACTOR SonicBush 11119
{
	Radius 24
	height 38
	- SOLID
	States
	{
		Spawn:
			SCBH A 1
			Loop
	}
}

// Sonic the Hedgehog tree prop
ACTOR SonicTree 11120
{
	Radius 5
	height 182
	+ SOLID
	States
	{
		Spawn:
			SCTR A 1
			Loop
	}
}

// Boxing glove weapon
ACTOR BoxingGlove : Weapon
{
	Weapon.Kickback 100
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	Obituary "%o was knocked out by %k"
	States
	{
		Ready:
			BXGI A 1 A_WeaponReady
			Loop
		Deselect:
			BXGI A 1 A_Lower
			Loop
		Select:
			BXGD A 0 A_Raise
			BXGD ABC 2 A_Raise
			BXGD C 0 A_PlaySound("DoomWare/BoxGloveHit")
			BXGD DEFGHIJLM 2 A_Raise
			Goto Ready
		Fire:
			BXGI A 0 A_Jump(120,"Attack1")
			BXGI A 0 A_Jump(120,"Attack2")
			BXGS ABC 2
			BXGS D 0 A_PlaySound("DoomWare/BoxGloveSwing3")
			BXGS D 2 A_CustomPunch(200, true)
			BXGS EFGHIJKLMNOP 2
			BXGI A 0 A_ReFire
			Goto Ready
		Attack1:
			BXGR AB 2
			BXGR C 2 A_CustomPunch(200, true)
			BXGR C 0 A_PlaySound("DoomWare/BoxGloveSwing1")
			BXGR DEFGHIJ 2
			BXGI A 0 A_ReFire
			Goto Ready
		Attack2:
			BXGL AB 2
			BXGL C 2 A_CustomPunch(400, true)
			BXGL C 0 A_PlaySound("DoomWare/BoxGloveSwing1")
			BXGL DEFGHIJK 2
			BXGI A 0 A_ReFire
			Goto Ready
	}
}

// Player won round flare particle emitter
Actor WonRoundEffect
{
	States
	{
	Spawn:
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
		TNT1 B 1
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,1), random(0,1), 8, random(-180,180), 0, 16)
		TNT1 B 2
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,1), random(0,1), 8, random(-180,180), 0, 16)
		TNT1 B 3
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,1), random(0,1), 8, random(-180,180), 0, 16)
		stop
	}
}

// Player lost round flare particle emitter
Actor FailedRoundEffect
{
	States
	{
	Spawn:
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FailFlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
		TNT1 B 1
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FailFlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,1), random(0,1), 8, random(-180,180), 0, 16)
		TNT1 B 2
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FailFlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,1), random(0,1), 8, random(-180,180), 0, 16)
		TNT1 B 3
		TNT1 BBBBBBBB 0 A_SpawnItemEx("FailFlareEffect", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,1), random(0,1), 8, random(-180,180), 0, 16)
		stop
	}
}

// Player won round flare particle
Actor FlareEffect
{
	projectile
	damage 0
	+CLIENTSIDEONLY
	-NOGRAVITY
	RenderStyle Add
	Scale 0.03
	Alpha 0.35
	States
	{
		Spawn:
			FLAR A 35 Bright
			Stop
	}
}

// Player lost round flare particle
Actor FailFlareEffect
{
	projectile
	damage 0
	+CLIENTSIDEONLY
	-NOGRAVITY
	Scale 0.03
	States
	{
		Spawn:
			FLAR B 35 Bright
			Stop
	}
}

// Confetti particle emitter
Actor ConfettiEffect
{
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 AAAAAA 0 A_SpawnItemEx("RedConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("GreenConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("BlueConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("YellowConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 A 5
			TNT1 AAAAAA 0 A_SpawnItemEx("RedConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("GreenConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("BlueConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("YellowConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 A 5
			TNT1 AAAAAA 0 A_SpawnItemEx("RedConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("GreenConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("BlueConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 AAAAAA 0 A_SpawnItemEx("YellowConfetti", Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(0,2), random(0,2), 8, random(-180,180), 0, 16)
			TNT1 A 5
			stop
	}
}

// Green confetti particle
Actor GreenConfetti
{
	projectile
	damage 0
	+THRUACTORS
	+CLIENTSIDEONLY
	-NOGRAVITY
	gravity 0.3
	States
	{
		Spawn:
			CONF AABCCB 2
			loop
	}
}

// Red confetti particle
Actor RedConfetti : GreenConfetti
{
	translation "112:127=176:191"
}

// Blue confetti particle
Actor BlueConfetti : GreenConfetti
{
	translation "112:127=199:207"
}

// Yellow confetti particle
Actor YellowConfetti : GreenConfetti
{
	translation "112:127=160:167"
}

// Absolutely nothing
Actor Nothing
{
	States
	{
		Spawn:
			TNT1 A 1
			stop
	}
}

// Duck Hunt tree prop
ACTOR DHuntTree 11121
{
	Radius 35
	height 124
	+ SOLID
	States
	{
		Spawn:
			DHTR A 1
			Loop
	}
}

// Duck Hunt bush prop
ACTOR DHuntBush 11122
{
	Radius 16
	height 37
	- SOLID
	States
	{
		Spawn:
			DHBS A 1
			Loop
	}
}

// NES Zapper weapon
ACTOR NESZapper : DoomWeapon
{
	attacksound "DoomWare/Zapper"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 1
	Weapon.AmmoType "Clip"
	Inventory.PickupMessage "You got a NES Zapper."
	Obituary "%o was zapped by %k"
	States
	{
		Ready:
			NESZ A 1 A_WeaponReady
			Loop
		Deselect:
			NESZ A 1 A_Lower
			Loop
		Select:
			NESZ E 1 A_Raise
			Loop
		Fire:
			NESZ A 0 ACS_ExecuteAlways(15,0)
			NESZ A 4 A_FireBullets(0,0,1,10,"Nothing",0)
			NESZ CDC 4
			NESZ A 0 A_ReFire
			Goto Ready
		Spawn:
			NESZ A -1
			Stop
	}
}

// Duck Hunt black duck
ACTOR Bird1
{
	Health 5
	PainChance 180
	Speed 10
	Radius 20
	Height 24
	//Mass 100
	Monster
	+FLOAT
	+DONTFALL
	+NOGRAVITY
	+NOBLOOD
	+Friendly
	ActiveSound "DoomWare/Quack"
	States
	{
		Spawn:
			BRD1 AB 10 A_Look
			Loop
		See:
			BRD1 AABB 2 A_Chase
			BRD1 B 0 A_PlaySound("DoomWare/Flap",6)
			BRD1 CCBB 2 A_Chase
			BRD2 B 0 A_PlaySound("DoomWare/Flap",6)
			Loop
		Pain:
			BRD1 D 2
			BRD1 D 2
			Goto See
		Death:
			BRD1 D 20 A_GiveToTarget("WonRoundItem",1)
			BRD1 E 0 A_ChangeFlag(DONTFALL,false)
			BRD1 E 0 A_ChangeFlag(FLOAT,false)
			BRD1 E 0 A_ChangeFlag(NOGRAVITY,false)
			BRD1 E 0 A_ChangeFlag(SOLID,false)
			BRD1 E 5
			BRD1 F 5
			BRD1 E 5
			BRD1 F 5
			BRD1 E 5
			Stop
	}
}

// Duck Hunt red duck
ACTOR Bird2 : Bird1
{
	States
	{
		Spawn:
			BRD2 AB 10 A_Look
			Loop
		See:
			BRD2 AABB 2 A_Chase
			BRD2 B 0 A_PlaySound("DoomWare/Flap",6)
			BRD2 CCBB 2 A_Chase
			BRD2 B 0 A_PlaySound("DoomWare/Flap",6)
			Loop
		Pain:
			BRD2 D 2
			BRD2 D 2
			Goto See
		Death:
			BRD2 D 20 A_GiveToTarget("WonRoundItem",1)
			BRD2 E 0 A_ChangeFlag(DONTFALL,false)
			BRD2 E 0 A_ChangeFlag(FLOAT,false)
			BRD2 E 0 A_ChangeFlag(NOGRAVITY,false)
			BRD2 E 0 A_ChangeFlag(SOLID,false)
			BRD2 E 5
			BRD2 F 5
			BRD2 E 5
			BRD2 F 5
			BRD2 E 5
			Stop
	}
}

// Duck Hunt blue duck
ACTOR Bird3 : Bird1
{
	States
	{
		Spawn:
			BRD3 AB 10 A_Look
			Loop
		See:
			BRD3 AABB 2 A_Chase
			BRD3 B 0 A_PlaySound("DoomWare/Flap",6)
			BRD3 CCBB 2 A_Chase
			BRD2 B 0 A_PlaySound("DoomWare/Flap",6)
			Loop
		Pain:
			BRD3 D 2
			BRD3 D 2
			Goto See
		Death:
			BRD3 D 20 A_GiveToTarget("WonRoundItem",1)
			BRD3 E 0 A_ChangeFlag(DONTFALL,false)
			BRD3 E 0 A_ChangeFlag(FLOAT,false)
			BRD3 E 0 A_ChangeFlag(NOGRAVITY,false)
			BRD3 E 0 A_ChangeFlag(SOLID,false)
			BRD3 E 5
			BRD3 F 5
			BRD3 E 5
			BRD3 F 5
			BRD3 E 5
			Stop
	}
}

// Rolling bouler projectile
ACTOR Boulder 11123
{
	SpawnID 255
	speed 64
	Projectile
	mass 100
	+NOBLOCKMAP
	-NOGRAVITY
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	obituary "%o thought he was Sisyphus."
	States
	{
		Spawn:
            BLD1 A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			BLD1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 A_Explode(512,52,1,1,48,0,0)
            BLD2 ABCDEF 2 A_Explode(512,52,1,1,48,0,0)
            BLD1 A 0 A_ChangeFlag(FLOORHUGGER,true)
            goto Move
        Move:
            BLD1 A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			BLD1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 2 A_Explode(512,52,1,1,48,0,0)
			BLD2 ABCDEF 2 A_Explode(512,52,1,1,48,0,0)
			Loop
	}
}

// Mecha Romero
ACTOR MechaRomero
{
	Health 3000
	Radius 30
	Height 62
	Mass 1000
	Speed 24
	PainChance 40
	Monster
	MinMissileChance 1
	+INVULNERABLE
	+BOSS
	+MISSILEMORE
	+MISSILEEVENMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	+NOINFIGHTING
	SeeSound "brain/sight"
	ActiveSound "brain/sight"
	PainSound "brain/pain"
	DeathSound "brain/death"
	Obituary "%o stood in awe of John Romero"
	States
	{
		Spawn:
			MCJR A 10 A_Look
			Loop
		See:
			MCJR A 6 A_Chase
			MCJR A 0 A_Metal
			MCJR B 6
			MCJR B 0 A_Metal
			MCJR B 0 A_JumpIfTargetInLOS("Missile")
			Loop
		Missile:
			MCJR C 20 Bright A_FaceTarget
			MCJR D 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			MCJR D 0 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 4 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			MCJR E 0 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 4 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 0 Bright A_SpidReFire
			Goto Missile +1
	}
}

// Mecha Romero (Float Wacky Mod)
ACTOR MechaRomero_Float : MechaRomero
{
	+FLOAT
	+NOGRAVITY
}

// Mecha Romero (Float Wacky Mod)
ACTOR MechaRomero_Rage : MechaRomero
{
	States
	{
		Missile:
			MCJR C 10 Bright A_FaceTarget
			MCJR D 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			MCJR D 0 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 2 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			MCJR E 0 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 2 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 0 Bright A_SpidReFire
			Goto Missile +1
	}
}

// Mecha Romero (Float Wacky Mod)
ACTOR MechaRomero_Spread : MechaRomero
{
	States
	{
		Missile:
			MCJR C 20 Bright A_FaceTarget
			MCJR D 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			MCJR D 0 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 4 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 0 Bright A_CustomBulletAttack(32.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 4 Bright A_CustomBulletAttack(32.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 0 Bright A_CustomBulletAttack(12.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 4 Bright A_CustomBulletAttack(12.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 0 bright A_PlaySound("chainguy/attack", CHAN_WEAPON)
			MCJR E 0 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 4 Bright A_CustomBulletAttack(22.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 0 Bright A_CustomBulletAttack(32.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 4 Bright A_CustomBulletAttack(32.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 0 Bright A_CustomBulletAttack(12.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR D 4 Bright A_CustomBulletAttack(12.5, 0, 1, random(15,20), "BulletPuff", 0, CBAF_NORANDOM)
			MCJR E 0 Bright A_SpidReFire
			Goto Missile +1
	}
}

// Flashing Cacodemon
ACTOR CacoTarget 11124
{
	mass 100000000
	Radius 31
	Height 56
	painchance 256
	+NOGRAVITY
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+FLOAT
    +DONTFALL
	health 2000000
	States
	{
		Spawn:
			HEAD A 1 A_SetTranslucent(0.5)
			Loop
		Pain:
			HEAD A 10 A_SetTranslucent(1)
			Goto Spawn
	}
}

// Explosion effect
ACTOR Explosion_Effect
{
	Radius 10
	Height 10
	Speed 1
	PROJECTILE
	+NOCLIP
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			MISL B 2
			MISL B 2 BRIGHT A_PlaySound("weapons/rocklx")
			MISL B 4 BRIGHT
			MISL C 6
			MISL D 4
			Stop
	}
}

// Explosion effect, but not as much noise
ACTOR Explosion_Effect_Quiet : Explosion_Effect
{
	States
	{
		Spawn:
			MISL B 2
			MISL B 2 BRIGHT A_PlaySound("weapons/rocklx", 0, 0.25)
			MISL B 4 BRIGHT
			MISL C 6
			MISL D 4
			Stop
	}
}

// Snow Speeder player morph
ACTOR SnowSpeederPlayer : DoomPlayer
{
	Player.ForwardMove 0
	Player.SideMove 0
	player.viewheight 12
	Player.JumpZ 0
	Health 100
	Radius 16
	Height 30
	Mass 100
	Player.ColorRange 112, 127
	PainSound ""
	Player.SoundClass "Hoth"
	DeathSound ""
	Player.MorphWeapon "SnowSpeederWeapon"
	+NOBLOOD
	+NOSKIN
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+SPAWNFLOAT
	States
	{
		Pain:
		Melee:
		Missile:
			Goto See
		See:
			Spawn:
			SSPD A 5
			Loop
		Death:
			MISL B 0 bright A_Playsound("weapons/rocklx")
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A -1
			Stop
		XDeath:
			Goto Death
	}
}

// Snow Speeder viewsprite weapon
ACTOR SnowSpeederWeapon : DoomWeapon
{
	Weapon.SelectionOrder 2000
	+WEAPON.DONTBOB
	+WEAPON.NOAUTOFIRE
	States
	{
		Ready:
			SSVW A 0 A_Recoil(-2)
			SSVW A 1 A_WeaponReady
			SSVW A 0 A_PlaySound("DoomWare/SnowSpeederEngine", 6, 0.5)
			loop
		Deselect:
			SSVW A 0 A_StopSound(6)
			SSVW A 1 A_Lower
			loop
		Select:
			SSVW A 1 A_Raise
			Loop
		Fire:
			SSVW A 0 A_PlaySound("DoomWare/SnowSpeederShoot", 7, 1)
			SSVW A 1 A_FireCustomMissile("SnowSpeederBlast",0,0,0,-12,0,1)
			SSVW A 0
			goto Ready
		AltFire:
			goto Ready
	}
}

// Snow Speeder blaster shot
ACTOR SnowSpeederBlast
{
	Radius 8
	Height 32
	Speed 100
	Damage 20
	Projectile
	Obituary "%o was blasted by %k"
	States
	{
		Spawn:
			SSBS A 1 Bright
			Loop
		Death:
			SSBS B 0 A_PlaySound("DoomWare/SnowSpeederExp")
			SSBS BCD 6 Bright
			Stop
	}
}

// Snow Speeder player morph (Sonic Speed)
ACTOR SnowSpeederPlayerSpeed : SnowSpeederPlayer
{
	Player.MorphWeapon "SnowSpeederWeaponFast"
}

// Snow Speeder viewsprite (Sonic Speed)
ACTOR SnowSpeederWeaponFast : SnowSpeederWeapon
{
	States
	{
		Ready:
			SSVW A 0 A_Recoil(-4)
			SSVW A 1 A_WeaponReady
			SSVW A 0 A_PlaySound("DoomWare/SnowSpeederEngine", 6, 0.5)
			loop
	}
}

// Outrider player morph
ACTOR OutriderPlayer : DoomPlayer
{
	Player.ForwardMove 0
	Player.SideMove 0
	player.viewheight 12
	Player.JumpZ 0
	Health 100
	Radius 16
	Height 30
	Mass 100
	Player.ColorRange 112, 127
	PainSound ""
	DeathSound ""
	Player.MorphWeapon "OutriderWeapon"
	Player.SoundClass "Hoth"
	+NOBLOOD
	+NOSKIN
	+FLOAT
	+NOGRAVITY
	+DONTFALL
	+SPAWNFLOAT
	States
	{
		Pain:
		Melee:
		Missile:
			Goto See
		See:
			Spawn:
			OTRR A 5
			Loop
		Death:
			MISL B 0 bright A_Playsound("weapons/rocklx")
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A -1
			Stop
		XDeath:
			goto Death
	}
}

// Outrider viewsprite weapon
ACTOR OutriderWeapon : DoomWeapon
{
	Weapon.SelectionOrder 2000
	+WEAPON.DONTBOB
	+WEAPON.NOAUTOFIRE
	States
	{
		Ready:
			ORVW A 0 A_Recoil(-1.5)
			ORVW A 1 A_WeaponReady
			ORVW A 0 A_PlaySound("DoomWare/SnowSpeederEngine", 6, 0.5)
			loop
		Deselect:
			ORVW A 0 A_StopSound(6)
			ORVW A 1 A_Lower
			loop
		Select:
			ORVW A 1 A_Raise
			Loop
		Fire:
			ORVW A 1
			goto Ready
		AltFire:
			goto Ready
	}
}

// Outrider player morph (Sonic Speed)
ACTOR OutriderPlayerSpeed : OutriderPlayer
{
	Player.MorphWeapon "OutriderWeaponFast"
}

// Outrider viewsprite (Sonic Speed)
ACTOR OutriderWeaponFast : OutriderWeapon
{
	States
	{
		Ready:
			ORVW A 0 A_Recoil(-3)
			ORVW A 1 A_WeaponReady
			ORVW A 0 A_PlaySound("DoomWare/SnowSpeederEngine", 6, 0.5)
			loop
	}
}

// Submarine player morph
Actor SubmarinePlayer : OutriderPlayer
{
    +NOSKIN
	-FLOAT
	-NOGRAVITY
	-DONTFALL
	-SPAWNFLOAT
    Scale 0.25
	Player.MorphWeapon "Outrider2Weapon"
	States
	{
		Pain:
		Melee:
		Missile:
			Goto See
		Spawn:
			SUBM A -1
			Loop
		See:
			SUBM ABCD 4
			Loop
		Death:
			MISL B 0 bright A_Playsound("weapons/rocklx")
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL B 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL C 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			MISL D 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(0, 16), Random(-16, 16), Random(0,359))
			TNT2 A -1
			Stop
		XDeath:
			goto Death
	}
}

// Submarine viewmodel wepaon
ACTOR Outrider2Weapon : OutriderWeapon
{
	States
	{
		Ready:
			SCVW A 1 A_WeaponReady
			loop
		Deselect:
			SCVW A 1 A_Lower
			loop
		Select:
			SCVW A 1 A_Raise
			Loop
	}
}

// Hoth AT-ST
ACTOR ATST
{
	Health 1500
	Radius 24
	scale 1.2
	Height 76
	Mass 1000
	Speed 8
	PainChance 0
	Monster
	+FLOORCLIP
	+NOBLOOD
	+MISSILEMORE
	+MISSILEEVENMORE
	HitObituary "$OB_KNIGHTHIT"
	Obituary "%o was gunned down by an AT-ST."
	States
	{
		Spawn:
			ATST A 10 A_Look
			Loop
		See:
			ATST A 0 A_PlaySound("DoomWare/ATSTWalk",5)
			ATST BBCCDDEE 9 A_Chase
			Loop
		Missile:
			ATST F 8 A_FaceTarget
			ATST G 0 A_PlaySound("DoomWare/ATSTShoot",1)
			ATST G 8 A_CustomMissile("ATSTBlast", 48)
			Goto See
		Pain:
		Death:
            ATST A 0 bright A_Playsound("weapons/rocklx")
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			ATST A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			TNT2 A 2 bright A_CustomMissile ("Explosion_Effect", Random(32, 64), Random(-16, 16), Random(0,359))
			Stop
	}
}

// Hoth AT-ST (Flight Wacky Mod)
ACTOR ATST_Flight : ATST
{
	+NOGRAVITY
	+FLOAT
}

// Hoth AT-ST (Rage Wacky Mod)
ACTOR ATST_Rage : ATST
{
	States
	{
		Missile:
			ATST F 4 A_FaceTarget
			ATST G 0 A_PlaySound("DoomWare/ATSTShoot",1)
			ATST G 4 A_CustomMissile("ATSTBlast", 48)
			Goto See
	}
}

// Hoth AT-ST (Spread Wacky Mod)
ACTOR ATST_Spread : ATST
{
	States
	{
		Missile:
			ATST F 8 A_FaceTarget
			ATST G 0 A_PlaySound("DoomWare/ATSTShoot",1)
			ATST G 0 A_CustomMissile("ATSTBlast", 48, 0, 10)
			ATST G 0 A_CustomMissile("ATSTBlast", 48, 0, -10)
			ATST G 8 A_CustomMissile("ATSTBlast", 48)
			Goto See
	}
}

// AT-ST blaster shot
ACTOR ATSTBlast
{
	Radius 8
	Height 32
	Speed 25
	Damage 20
	Projectile
	Obituary "%o was gunned down by an AT-ST."
	States
	{
		Spawn:
			SSBS A 1 Bright
			Loop
		Death:
			SSBS B 0 A_PlaySound("DoomWare/SnowSpeederExp")
			SSBS BCD 6 Bright
			Stop
	}
}

// Hoth turret blaster emitter
ACTOR HothTurret
{
	Health 50000000
	Radius 24
	Height 8
	Speed 0
	PainChance 0
	Monster
	+FRIENDLY
	+MISSILEMORE
	+MISSILEEVENMORE
	+Float
	+NoGravity
	+NOBLOOD
	HitObituary "%o was accidentally shot down by an ally Turret."
	States
	{
		Spawn:
			TNT2 AB 10 A_Look
			Loop
		See:
			TNT2 AABBCCDD 3 A_Chase
			Loop
			Missile:
			TNT2 EF 8 A_FaceTarget
			TNT2 G 0 A_PlaySound("DoomWare/HothTurShoot")
			TNT2 G 8 A_CustomMissile("HothTurretBlast")
			Goto See
		Pain:
			TNT2 H 2
			TNT2 H 2 A_Pain
			Goto See
		Death:
			Stop
	}
}

// Hoth turret blaster shot
ACTOR HothTurretBlast : ATSTBlast
{
	HitObituary "%o was accidentally shot down by an ally Turret."
	States
	{
		Spawn:
			HTRS A 1 Bright
			Loop
		Death:
			HTRS B 0 A_PlaySound("DoomWare/SnowSpeederExp")
			HTRS BCD 6 Bright
			Stop
	}
}

// 1-UP item
ACTOR 1up : Ammo 11125
{
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 10000
	Inventory.PickupSound ""
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10000
	states
	{
		Spawn:
			1ups A 4
			Loop
		Death:
			1ups A 0
			stop
	}
}

// Golden Axe Thief
ACTOR BlueThief
{
	Health 60
	Radius 20
	Height 56
	Mass 100
	Speed 28
	PainChance 256
	Monster
	+FLOORCLIP
	+FRIGHTENED
	+LOOKALLAROUND
	translation "112:127=195:207" , "11:12=240:241"
	HitObituary ""
	seesound "DoomWare/ThiefSee"
	Obituary ""
	dropitem "healthbonus"
	States
	{
		Spawn:
			SANT AB 10 A_Look
			Loop
		See:
			SANT AABBCCDD 2 A_Chase
			SANT EFEFEFEFEFEF 4
			Loop
		Pain:
			SANT G 0 A_PlaySound("DoomWare/ThiefHurt")
			SANT G 0 a_spawnitem("healthbonus")
			SANT GHIJKL 2
			Goto See
		Death:
			SANT G 0 A_PlaySound("DoomWare/ThiefDie")
			SANT G 1 a_noblocking
			SANT GHIJKL 4 a_fadeout(0.2)
			Stop
	}
}

// Golden Axe Thief (Flight Wacky Mod)
ACTOR BlueThief_Flight : BlueThief
{
	+NOGRAVITY
	+FLOAT
}

// Golden Axe Thief (Spread Wacky Mod)
ACTOR BlueThief_Spread : BlueThief
{
	// Exactly the same
}

// Golden Axe Thief (Rage Wacky Mod)
ACTOR BlueThief_Rage : BlueThief
{
	// Exactly the same
}

// Peanut Butter Jelly Banana
ACTOR Banana
{
	Monster
	Radius 16
	Height 32
	Mass 100
	speed 20
	+FLOORCLIP
	+FLOORHUGGER
	-SOLID
	Obituary "%o does not like cereal."
	States
	{
		Spawn:
			BANA AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FloorProtrude",0,0,random(0,360),2,random(-20,90))
			BANA I 2 A_PlaySound("DoomWare/Cereal")
			BANA JKLMNOPQRSTUV 2
			BANA DEFGHAB 3 A_CHASE
			Goto Death
		Death:
			BBOM A 0 A_Explode(512,256)
			BBOM ABCDEFGHIJKLMNOPQRSTUVWX 2
			TNT1 A 1
			Stop
	}
}

// Floor portrusion effect
ACTOR FloorProtrude
{
	PROJECTILE
	Damage 0
	Height 2
	radius 2
	+CLIENTSIDEONLY
	-NOGRAVITY
	Speed 10
	States
	{
		Spawn:
			MARB AABBCCDDAABBCCDDAABBCCDDAABBCCDD 1
			Stop
		Death:
			MARB A 1
			Stop
	}
}

// AVOID warning sign that moves
ACTOR Avoid
{
	Health 10000
	PainChance 180
	Speed 20
	Radius 32
	Height 64
	Mass 9999999999
	Monster
	+FLOORCLIP
	-SOLID
	+NOBLOOD
	+LOOKALLAROUND
	States
	{
		Spawn:
			AVOD A 1
			AVOD A 0
			Goto See
		See:
			AVOD A  0 A_PlaySound("DoomWare/AvoidMove")
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase
			AVOD AA 2 A_Chase // Yes I know this looks disgusting, but please spare me. The issue is that A_StopSound doesn't stop looping sounds. This was the only way to get Doomware/AvoidMove to loop and be able to be stopped when needed.
			Loop
		Death:
			TNT1 A 5 A_PlaySound("DoomWare/AvoidHere")
			AVOD A 5
			TNT1 A 5
			AVOD A 5
			TNT1 A 5
			AVOD A 5
			TNT1 A 5
			AVOD A 5
			TNT1 A 5
			AVOD A 5
			TNT1 A 5
			stop
	}
}

// Floor spike
Actor LilithSpikeSpawner
{
	Health 10000000000
	Radius 16
	Height 93
	Speed 0
	Damage 0
	Alpha 0.75
	Mass 9999999999
	Obituary "%o was impaled."
	renderstyle translucent
	Translation "192:207=112:127"
	Monster
	- Solid
	+ shootable
	+ Noblood
	- NOBLOCKMAP
	+ NoTarget
	States
	{
		Spawn:
			TSPK A 4 Bright A_NoBlocking
			TSPK A 0 A_PlaySound("DoomWare/Spike")
			TSPK B 4 Bright A_Explode(500,32)
			TSPK BBBBBBBBBBBBBBBBBB 0 A_CustomMissile("FloorProtrude",0,0,random(0,360),2,random(-20,90))
			TSPK C 4 Bright A_Explode(500,32)
			TSPK D 0 A_Explode(500,32)
			TSPK D 4 Bright A_ChangeFlag("SOLID", 1)
			TSPK E 35 Bright
			Goto Death
		Death:
			MNS2 A 0 A_ChangeFlag("SOLID", 0)
			MNS2 ABCDEFGHIJKLMNOPQ 1 Bright
			Stop
	}
}

// Basic hat actor
ACTOR Hat
{
	Radius 8
	height 10
	-SOLID
	+NOGRAVITY
}

// Winner hat
ACTOR Crown : Hat
{
	States
	{
		Spawn:
			WINR A 16 Bright
			Loop
	}
}

// Beta tester hat
ACTOR BetaHat : Hat
{
	States
	{
		Spawn:
			BETA A 16
			Loop
	}
}

// Buu342 hat
ACTOR Buu342Hat : Hat
{
	scale 0.7
	States
	{
		Spawn:
			BUUH A 16
			Loop
	}
}

// 250+ points player hat
ACTOR RegularHat : Hat
{
	States
	{
		Spawn:
			REGU A 16
			Loop
	}
}

// Achiever hat
ACTOR AchieverHat : Hat
{
	States
	{
		Spawn:
			ACHV A 16
			Loop
	}
}

// Leader hat
ACTOR LeaderHat : Hat
{
	States
	{
		Spawn:
			LEAD A 16 Bright
			Loop
	}
}

// AVOID warning sign that stays still
ACTOR AvoidDecoration
{
	Radius 8
	height 10
	- SOLID
	States
	{
		Spawn:
			AVOD A 16
			Loop
	}
}

// Lillith (bullet hell monster)
Actor Lilith 8735
{
	Health 66666
	Radius 64
	Height 100
	Speed 10
	Scale 1.5
	Mass 100000
	Damage 50
	+Float
	+NoGravity
	+FloatBob
	HitObituary "%o was eviscerated by Lilith."
	Obituary "%o was eviscerated by Lilith."
	States
	{
		Spawn:
			ARNQ A 1 A_PlaySound("DoomWare/LilithSee")
			goto See
        See:
			ARNQ A 1
			ARNQ A 0 A_CustomMissile("OrbitBall",90, 0, 0)
			ARNQ A 0 A_CustomMissile("OrbitBall2", 90, 0, 180)
			goto Missile
		Missile:
			ARNQ ABC 2 A_PlaySound("DoomWare/LilithAttack")
			ARNQ D 2 A_SetAngle(270)
			ARNQ E 0 bright A_CustomMissile ("LilithBall1", 70, 0, random(-25,-15), CMF_AIMDIRECTION, -2)
			ARNQ E 0 bright A_CustomMissile ("LilithBall1", 70, 0, random(-15,-5), CMF_AIMDIRECTION, -2)
			ARNQ E 0 bright A_CustomMissile ("LilithBall1", 70, 0, random(-5,5), CMF_AIMDIRECTION, -2)
			ARNQ E 0 bright A_CustomMissile ("LilithBall1", 70, 0, random(5,15), CMF_AIMDIRECTION, -2)
			ARNQ E 0 bright A_CustomMissile ("LilithBall1", 70, 0, random(15,25), CMF_AIMDIRECTION, -2)
			ARNQ E 2 bright
			Goto Missile +1
	}
}

// Lilith orbit ball effect
ACTOR OrbitBall
{
     PROJECTILE
     SeeSound ""
     Scale 1
     Radius 6
     Height 8
     RenderStyle Add
     Alpha 0.5
     Speed 5
     MissileHeight 0
     +NOCLIP
     States
     {
		 Spawn:
			 MSP1 AABBCCDD 1 Bright A_MissileAttack
			 MSP1 A 0 A_CustomMissile("OrbitBall", 0, 0, 75)
			 MSP1 A 0 A_MissileAttack
			 Stop
     }
}

// Lilith orbit ball effect (opposite direction=
ACTOR OrbitBall2 : OrbitBall
{
     PROJECTILE
     +NOCLIP
     MissileHeight 0
     States
     {
		 Spawn:
			 MSP1 AABBCCDD 1 Bright A_MissileAttack
			 MSP1 A 0 A_CustomMissile("OrbitBall2", 0, 0, -75)
			 MSP1 A 0 A_MissileAttack
			 Stop
     }
}

// Lillith mouth projectile
Actor LilithBall1
{
	Radius 10
	Height 8
	Speed 25
	Damage 10
	Projectile
	Obituary "%o was eviscerated by Lilith."
	RenderStyle Add
	Alpha 0.9
	SeeSound "Imp/Attack"
	DeathSound "Imp/ShotX"
	Decal Scorch
	States
	{
		Spawn:
			HECF A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			HECF AB 4 Bright
			MISL A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_CheckCleanup", 0)
			Loop
		Death:
			HECF CDE 4 Bright
			Stop
	}
}

// Dangerously hot chilly
ACTOR Chilli
{
	Radius 16
	Height 32
	Mass 100
	speed 0
	monster
	+BOSS
	+FLOORCLIP
	+LOOKALLAROUND
	-SOLID
	-PUSHABLE
	+NOINTERACTION
	SeeSound ""
	Obituary "%o found the chilli too spicy."
	States
	{
		Spawn:
			CHIL A 1 A_LookEx(LOF_FULLVOLSEESOUND, 0, 0, 0, 0, "See")
			Goto See
		See:
			CHIL A 52 A_PlaySound("DoomWare/WTFBoom")
			Goto Death
		Death:
			BBOM A 0 A_Explode(512,256)
			BBOM AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("SmallFlames", 32.0, 0, random(0,360), 2, random(0, 80))
			BBOM ABCDEFGHIJKLMNOPQRSTUVWX 4
			TNT1 A 1
			Stop
	}
}

// Explosive barrel with legs
ACTOR ExplosiveBarrelLegs
{
	Health 20
	Radius 20
	Height 56
	Mass 100
	Speed 12
	painchance 256
	Monster
	+FLOORCLIP
	+NOBLOOD
	+LOOKALLAROUND
	+DONTGIB
	-SOLID
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	States
	{
		Spawn:
			BAR1 A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			BAR1 AB 10
			Goto Wake
		Wake:
			MISL A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			BALE A 3
			Goto See
		See:
			BALE A 0 A_Jump(96,"Rest")
			MISL A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			BALE BCD 3 A_Chase
			Loop
		Rest:
			BAR1 A 3
			MISL A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			Goto Spawn
		Death:
			BAR1 A 0 A_GiveToTarget("FailRoundItem",1)
			BAR1 A 0 A_ChangeFlag("SOLID",false)
			BEXP A 5 Bright
			BEXP B 5 Bright A_Scream
			BEXP C 5 Bright
			BEXP D 5 Bright
			BEXP E 10 Bright
			stop
		xDeath:
			TNT1 A 1
			stop
	}
}

// Non moving explosive barrel
ACTOR ExplosiveBarrelReal
{
	Health 20000
	Radius 20
	Height 56
	Mass 10000
	Speed 0
	painchance 256
	Monster
	+FLOORCLIP
	+NOBLOOD
    +QUICKTORETALIATE
	+DONTGIB
	+OLDRADIUSDMG
	+SOLID
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	States
	{
		Spawn:
			BAR1 A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			BAR1 AB 10
			BAR1 A 0 ACS_NamedExecuteAlways ("DoomWare_Entity_CheckCleanup", 0)
			Loop
        Pain:
			BAR1 A 1 ACS_ExecuteAlways(10, 0)
			Goto Spawn
		Death:
			TNT1 A 1
			stop
	}
}

// Hissy
ACTOR Hissy 8734
{
	Radius 21
	Height 17
	+SOLID
	States
	{
		Spawn:
			HISS A 1
			Loop
	}
}

// Linguica for Free Hissy level
ACTOR Linguica
{
	Health 150
	Speed 20
	Mass 120
	ReactionTime 1
	PainChance 00
	Radius 24
	Height 60
	Scale 1.1
 	Monster
  	+FLOORCLIP
  	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+LOOKALLAROUND
	-SHOOTABLE
	-SOLID
	SeeSound "baron/sight"
	Obituary "%o tried to free hissy, and failed."
	States
	{
		Spawn:
			HHGR A 1 A_Look
			HHGR A 0
			HHGR AB 10 A_Look
			Goto Spawn+2
		See:
			HHGR AABBCCDD 2 A_Chase
			Loop
		Missile:
			HHGR EE 5 A_FaceTarget
			HHGR F 0 A_PlaySound ("Weapons/GrenLF")
			HHGR F 5 A_ThrowGrenade("Grenade", 0, 28, 1)
			Goto See
	}
}

// Linguica (Flight Wacky Mod)
ACTOR Linguica_Flight : Linguica
{
	+FLOAT
	+NOGRAVITY
}

// Linguica (Spread Wacky Mod)
ACTOR Linguica_Spread : Linguica
{
	States
	{
		Missile:
			HHGR EE 5 A_FaceTarget
			HHGR F 0 A_PlaySound ("Weapons/GrenLF")
			HHGR F 0 A_SetAngle(angle+10)
			HHGR F 0 A_ThrowGrenade("Grenade", 0, 28, 1)
			HHGR F 0 A_SetAngle(angle-20)
			HHGR F 0 A_ThrowGrenade("Grenade", 0, 28, 1)
			HHGR F 0 A_SetAngle(angle+10)
			HHGR F 5 A_ThrowGrenade("Grenade", 0, 28, 1)
			Goto See
	}
}

// Linguica (Rage Wacky Mod)
ACTOR Linguica_Rage : Linguica
{
	States
	{
		Missile:
			HHGR EE 2 A_FaceTarget
			HHGR F 0 A_PlaySound ("Weapons/GrenLF")
			HHGR F 2 A_ThrowGrenade("Grenade", 0, 28, 1)
			Goto See
	}
}

// Lost Soul for counting minigame
ACTOR SkullCounter : LostSoul
{
	-SOLID
	Height 1
	radius 1
	States
	{
		Spawn:
			SKUL C 1 bright
			Goto Missile
		See:
			SKUL A 1 bright
			Goto Death
		Missile:
			SKUL D 0 bright ThrustThing(0,10,0,0)
			SKUL D 0 bright a_playsound("skull/melee")
			SKUL D 4 bright A_SkullAttack
			SKUL CD 4 bright
			Goto Missile + 2
		Death:
			SKUL E 1 bright
			stop
	}
}

// Fake Rocket for counting minigame
ACTOR FakeRocket : Rocket
{
	Radius 1
	Height 1
	Damage 0
	-SOLID
	States
	{
		Death:
			MISL B 8 Bright
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}

// Invisible death item (used in hurdles)
ACTOR InvisibleBridge8Killer : InvisibleBridge 11126
{
	Radius 8
	Height 1
	States
	{
		Spawn:
			TNT1 A 1 A_Explode(100,8,0,0,8)
			Loop
	}
}

// Chicken cross the road car
ACTOR DangerousCar
{
	Radius 32
	Height 32
	Speed 20
	Damage 100
	Projectile
	+ROCKETTRAIL
	+RIPPER
	Obituary "%o didn't look both ways before crossing"
	States
	{
		Spawn:
			LGCR A 1 Bright
			Loop
	}
}

// Mighty Foot (Attack a thief)
ACTOR Boot : DoomWeapon
{
	Weapon.SelectionOrder 3500
	Weapon.KickBack 150
	Weapon.YAdjust -8
	Obituary "%o was kicked by %k"
	+BLOODSPLATTER
	+WEAPON.NOAUTOFIRE
	States
	{
		Select:
			TNT1 A 1 A_Raise
			Loop
		Deselect:
			TNT1 A 1 A_Lower
			Loop
		Ready:
			TNT1 A 1 A_WeaponReady
			Loop
		Fire:
			BOOT A 4
			BOOT B 4 A_FireBullets(0, 0, 1, 3, "KickPuff", FBF_NOFLASH | FBF_NORANDOM, 64) // A_CustomPunch(20, true, CPF_NOTURN) // CPF_NOTURN Flag unsupported in Zandronum :/
			BOOT A 4
			Goto Ready
	}
}

// Boot bullet puff
ACTOR KickPuff : BulletPuff
{
    +PUFFONACTORS
	States
	{
		Spawn:
			PUFF C 4 
			PUFF D 4 A_PlaySound("player/male/fist")
			Stop
	}
}

// "Don't Get Wet" umbrella
ACTOR Umbrella 11127
{
	Radius 64
	height 152
	- SOLID
	States
	{
		Spawn:
			UMBR A 16 BRIGHT
			Loop
	}
}

// Flame projectile
ACTOR FlameProjectile
{
	Radius 6
	Height 8
	Speed 15
	Damage 2
	Projectile
	+RANDOMIZE
	+RIPPER
	RenderStyle Add
	Alpha 1
	States
	{
		Spawn:
			FLME ABCDE 4 BRIGHT
			TNT1 A 4
			stop
	}
}

// Flamethrower projectile launcher
ACTOR FlameProjectiler 11128
{
	Health 150
	Speed 20
	Mass 120
	ReactionTime 1
	PainChance 00
	Radius 8
	Height 16
	Scale 1
 	Monster
  	+FLOORCLIP
  	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+LOOKALLAROUND
	-SHOOTABLE
	-SOLID
	Obituary "%o didn't realize fire is hot."
	States
	{
		Spawn:
			FLMR A 1
			Goto See
		See:
			FLMR A 0 A_PlaySound("DoomWare/FlameThrower")
			FLMR AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_ThrowGrenade("FlameProjectile",-20.0,0,random(-1,1))
            FLMR A 70
			Loop
	}
}

// 1-Up text effect
ACTOR 1UPText
{
	+NOGRAVITY
	+FloatBOB
	-SOLID
	States
	{
		Spawn:
			1UPT A 1 BRIGHT
			Goto Death
		Death:
			1UPT A 0 ThrustThingZ(0, 16, 0, 0)
			1UPT A 35 BRIGHT
			1UPT A 0
			Stop
	}
}

// Pokemon Viridian Forest sign
ACTOR PokemonSign 11130
{
	Radius 8
	height 64
	+SOLID
	States
	{
		Spawn:
			PKSN A 1
			Loop
	}
}

// Pokemon Viridian Forest tree
ACTOR PokemonTree 11131
{
	Radius 32
	height 64
	+SOLID
	States
	{
		Spawn:
			PKTR A 1
			Loop
	}
}

// Pokemon Viridian Forest tree stump
ACTOR PokemonStump 11132
{
	Radius 16
	height 64
	+SOLID
	States
	{
		Spawn:
			PKTR B 1
			Loop
	}
}

// Pokemon encounter item
ACTOR PokemonTrigger : Ammo 11133
{
	radius 2
	Inventory.PickupMessage ""
	Inventory.PickupSound ""
	Inventory.Amount 1
	Inventory.MaxAmount 10000
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10000
	states
	{
		Spawn:
			TNT1 A 4
			Loop
		Death:
			TNT1 A 0
			stop
	}
}

// Pokemon Ash player morph
ACTOR AshPlayer : DoomPlayer
{
	Player.SoundClass ""
	Player.ForwardMove 0.4, 0.4
	Player.SideMove 0.3, 0.3
	player.viewheight 18
	Player.JumpZ 0
	Health 100
	Radius 16
	Height 14
	+NOSKIN
	States
	{
		Spawn:
			ASHP A -1
			Loop
		See:
			ASHP ABAC 4
			Loop
	}
}

// Pokemon food plate
ACTOR Food_Plate 11129
{
	Radius 32
	height 16
	States
	{
		Spawn:
			PLTE A 1
			Loop
	}
}

// Pokemon egg roll food item
ACTOR Food_EggRoll : Ammo 11134
{
	Inventory.PickupMessage "Picked up an egg roll"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			EGRL A 1
			Loop
	}
}

// Pokemon red salmon roll food item
ACTOR Food_SalmonRoll1 : Ammo 11135
{
	Inventory.PickupMessage "Picked up a salmon roll"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			SALR A 1
			Loop
	}
}

// Pokemon orange salmon roll food item
ACTOR Food_SalmonRoll2 : Ammo 11136
{
	Inventory.PickupMessage "Picked up a salmon roll"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			SURR A 1
			Loop
	}
}

// Pokemon tea food item
ACTOR Food_Tea : Ammo 11137
{
	Inventory.PickupMessage "Picked up some warm tea"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			TEAA A 1
			Loop
	}
}

// Pokemon green shrimp food item
ACTOR Food_Sushi1 : Ammo 11138
{
	Inventory.PickupMessage "Picked up some sushi"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			SHI1 A 1
			Loop
	}
}

// Pokemon red sushi food item
ACTOR Food_Sushi2 : Ammo 11139
{
	Inventory.PickupMessage "Picked up some sushi"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			SHI2 A 1
			Loop
	}
}

// Pokemon toro food item
ACTOR Food_Toro : Ammo 11140
{
	Inventory.PickupMessage "Picked up some toro"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			TORO A 1
			Loop
	}
}

// Pokemon shrimp food item
ACTOR Food_Shrimp : Ammo 11141
{
	Inventory.PickupMessage "Picked up some shrimp"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			SHMP A 1
			Loop
	}
}

// Runescape pickaxe weapon
ACTOR PickaxeWeapon : MaceWeapon
{
	States
	{
		Select:
			RSPK A 1 A_Raise
			Loop
		Deselect:
			RSPK A 1 A_Lower
			Loop
		Ready:
			RSPK A 1 A_WeaponReady
			Loop
		Fire:
			RSPK B 2 Offset(60, 20)
			RSPK B 1 Offset(30, 33)
			RSPK B 2 Offset(8, 45)
			RSPK C 1 Offset(8, 45)
			RSPK D 1 Offset(8, 45)
			RSPK E 1 Offset(8, 45) 
			RSPK E 1 Offset(-11, 58) A_FireBullets(0, 0, 1, 20, "MeleePuff", FBF_NOFLASH | FBF_NORANDOM, 64) // A_CustomPunch(20, true, CPF_NOTURN) // CPF_NOTURN Flag unsupported in Zandronum :/
			RSPK F 1 Offset(8, 45)
			RSPK F 2 Offset(-8, 74)
			RSPK F 1 Offset(-20, 96)
			RSPK F 8 Offset(-33, 160)
			RSPK A 2 Offset(8, 75) A_ReFire
			RSPK A 1 Offset(8, 65)
			RSPK A 2 Offset(8, 60)
			RSPK A 1 Offset(8, 55)
			RSPK A 2 Offset(8, 50)
			RSPK A 1 Offset(8, 45)
			Goto Ready
	}
}

// Tin ore rock
ACTOR TinOre 11142
{
	Health 40
	Radius 16
	Height 72
	Mass 1000000
	Speed 0
	PainChance 256
	SeeSound ""
	PainSound "DoomWare/PickaxeHit"
	DeathSound "DoomWare/PickaxeCollect"
	ActiveSound ""
	+FLOORCLIP
	+NoBlood
	+SHOOTABLE
	+SOLID
	States
	{
		Spawn:
			RSOR A 10 A_Look
			Loop
		See:
			RSOR A 3
			Loop
		Pain:
			RSOR A 0 A_PAIN
			goto See
		Death:
			RSOR B 0 A_GiveToTarget("TinOreItem",1)
			RSOR B 0 A_Scream
			RSOR B 0 A_ChangeFlag("shootable",1)
			RSOR B 0 A_ChangeFlag("SOLID",1)
			RSOR B 0 A_CustomMissile("SolidCorpse")
			RSOR B 175 A_Scream
			stop
	}
}

// Gold ore rock
ACTOR GoldOre : TinOre 11143
{
	States
	{
		Spawn:
			RSOR C 10 A_Look
			Loop
		See:
			RSOR C 3
			Loop
		Pain:
			RSOR C 0 A_PAIN
			goto See
		Death:
			RSOR B 0 A_GiveToTarget("GoldOreItem",1)
			RSOR B 0 A_Scream
			RSOR B 0 A_ChangeFlag("shootable",1)
			RSOR B 0 A_ChangeFlag("SOLID",1)
			RSOR B 0 A_CustomMissile("SolidCorpse")
			RSOR D 175 A_Scream
			stop
	}
}

// Mithril ore rock
ACTOR MithrilOre : TinOre 11144
{
	States
	{
		Spawn:
			RSOR E 10 A_Look
			Loop
		See:
			RSOR E 3
			Loop
		Pain:
			RSOR E 0 A_PAIN
			goto See
		Death:
			RSOR B 0 A_GiveToTarget("MithrilOreItem",1)
			RSOR B 0 A_Scream
			RSOR B 0 A_ChangeFlag("shootable",1)
			RSOR B 0 A_ChangeFlag("SOLID",1)
			RSOR B 0 A_CustomMissile("SolidCorpse")
			RSOR B 175 A_Scream
			stop
	}
}

// Solid corpse for Runescape ore
ACTOR SolidCorpse
{
	Radius 16
	Height 128
	Mass 1000000
	Speed 0
	PainChance 256
	SeeSound ""
	ActiveSound ""
	+FLOORCLIP
	+NoBlood
	+SHOOTABLE
	+SOLID
	States
	{
		Spawn:
			TNT1 A 175
			stop
	}
}

// Tin ore item
ACTOR TinOreItem : Ammo
{
	Inventory.PickupMessage "Mined some Tin"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.PickupSound "DoomWare/Ring"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0
			stop
	}
}

// Mithril ore item
ACTOR MithrilOreItem : Ammo
{
	Inventory.PickupMessage "Mined some Mithril"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.PickupSound "DoomWare/Ring"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0
			stop
	}
}

// Gold ore item
ACTOR GoldOreItem : Ammo
{
	Inventory.PickupMessage "Mined some Gold"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.PickupSound "DoomWare/Ring"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 10
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0
			stop
	}
}

// Runescape player morph
ACTOR RunescapePlayer : DoomPlayer
{
	+NOSKIN
	+NODAMAGE
	+NOBLOODDECALS
	BloodType "RunescapeSplat"
	Player.SoundClass "Bob"
	Player.ColorRange 112, 127
	Scale 0.5
	States
	{
		Spawn:
			RUNP A -1
			Loop
		See:
			RUNP ABCB 4
			Loop
		Missile:
			RUNP DDEFGFED 3
			Goto Spawn
	}
}

// Runescape blood splat
ACTOR RunescapeSplat
{
	Scale 0.5
	vspeed 0 // Does nothing?
	+NOGRAVITY // No way to make a blood splat keep still, apparently?
	+NOBLOCKMAP
	+NOTELEPORT
	States
	{
		Spawn:
			HTSP A 16
			Stop
	}
}
// Eclipse (for skybox)
ACTOR Eclipse 11145
{
	Radius 27
	Height 53
	+NOGRAVITY
	States
	{
		Spawn:
			ECPS A 1 Bright
			Loop
	}
}

// FreeDoom Tentacle Worm for Eclipse level
ACTOR TentacleWorm : Demon
{
	attacksound "DoomWare/EclipseBeastAttack"
	seesound "DoomWare/EclipseBeastSpot"
	painchance 0
	speed 20
	+NoInfighting
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
	States
	{
		Spawn:
			TENT AB 10 A_Look
			Loop
		See:
			TENT AABBCCDD 2 A_Chase
			Loop
		Melee:
			TENT EF 4 A_FaceTarget
			TENT G 4 A_SargAttack
			Goto See
		Death:
			TNT1 A 1
			stop
	}
}

// FreeDoom Tentacle Worm (Flight Wacky Mod)
ACTOR TentacleWorm_Flight : TentacleWorm
{
	+FLOAT
	+NOGRAVITY
}

// FreeDoom Tentacle Worm (Rage Wacky Mod)
ACTOR TentacleWorm_Rage : TentacleWorm
{
	States
	{
		Melee:
			TENT EF 2 A_FaceTarget
			TENT G 2 A_SargAttack
			Goto See
	}
}

// FreeDoom Tentacle Worm (Spread Wacky Mod)
ACTOR TentacleWorm_Spread : TentacleWorm
{
	// No changes
}

// FreeDoom worm for Eclipse level
ACTOR SlimyWorm : Demon
{
	attacksound "DoomWare/EclipseBeastAttack"
	seesound "DoomWare/EclipseBeastSpot"
	painchance 0
	speed 20
	+NoInfighting
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
	States
	{
		Spawn:
			SWRM AB 10 A_Look
			Loop
		See:
			SWRM AABBCCDD 2 A_Chase
			Loop
		Melee:
			SWRM EF 4 A_FaceTarget
			SWRM G 4 A_SargAttack
			Goto See
		Death:
			TNT1 A 1
			stop
	}
}

// FreeDoom worm (Flight Wacky Mod)
ACTOR SlimyWorm_Flight : SlimyWorm
{
	+FLOAT
	+NOGRAVITY
}

// FreeDoom worm (Rage Wacky Mod)
ACTOR SlimyWorm_Rage : SlimyWorm
{
	States
	{
		Melee:
			SWRM EF 2 A_FaceTarget
			SWRM G 2 A_SargAttack
			Goto See
	}
}

// FreeDoom worm (Spread Wacky Mod)
ACTOR SlimyWorm_Spread : SlimyWorm
{
	// No changes
}

// Cool looking imp for Eclipse level
ACTOR Aguares : Demon
{
	attacksound "DoomWare/EclipseBeastAttack"
	seesound "DoomWare/EclipseBeastSpot"
	painchance 0
	speed 20
	+NoInfighting
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
	States
	{
		Spawn:
			AGUR AB 10 A_Look
			Loop
		See:
			AGUR AABBCCDD 2 A_Chase
			Loop
		Melee:
			AGUR WX 4 A_FaceTarget
			AGUR Y 4 A_SargAttack
			Goto See
		Death:
			TNT1 A 1
			stop
	}
}

// Aguares (Flight Wacky Mod)
ACTOR Aguares_Flight : Aguares
{
	+FLOAT
	+NOGRAVITY
}

// Aguares (Rage Wacky Mod)
ACTOR Aguares_Rage : Aguares
{
	States
	{
		Melee:
			AGUR WX 2 A_FaceTarget
			AGUR Y 2 A_SargAttack
			Goto See
	}
}

// Aguares (Spread Wacky Mod)
ACTOR Aguares_Spread : Aguares
{
	// No changes
}

// Cuthulu looking monster for Eclipse level
ACTOR ForbiddenOne : Demon
{
	attacksound "DoomWare/EclipseBeastAttack"
	seesound "DoomWare/EclipseBeastSpot"
	painchance 0
	speed 20
	+NoInfighting
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
	States
	{
		Spawn:
			CUTH AB 10 A_Look
			Loop
		See:
			CUTH AABBCCDD 2 A_Chase
			Loop
		Melee:
			CUTH EFGH 2 A_FaceTarget
			CUTH I 2 A_SargAttack
			Goto See
		Death:
			TNT1 A 1
			stop
	}
}

// Forbidden One (Flight Wacky Mod)
ACTOR ForbiddenOne_Flight : ForbiddenOne
{
	+NOGRAVITY
	+FLOAT
}

// Forbidden One (Spread Wacky Mod)
ACTOR ForbiddenOne_Rage : ForbiddenOne
{
	States
	{
		Melee:
			CUTH EFGH 1 A_FaceTarget
			CUTH I 1 A_SargAttack
			Goto See
	}
}

// Forbidden One (Spread Wacky Mod)
ACTOR ForbiddenOne_Spread : ForbiddenOne
{
	// No changes
}

// Recolored Demon for Eclipse level
ACTOR Demon3 : Demon
{
	translation "32:47=40:47", "16:31=32:39", "208:223=44:47"
	+NoInfighting
	speed 20
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
}

// Recolored Demon (Flight Wacky Mod)
ACTOR Demon3_Flight : Demon3
{
	+FLOAT
	+NOGRAVITY
}

// Recolored Demon (Rage Wacky Mod)
ACTOR Demon3_Rage : Demon3
{
	States
	{
		Melee:
			SARG EF 2 A_FaceTarget
			SARG G 2 A_SargAttack
			Goto See
	}
}

// Recolored Demon (Spread Wacky Mod)
ACTOR Demon3_Spread : Demon3
{
	// No changes
}

// Recolored Cacodemon for Eclipse level
ACTOR Cacodemon2 : Cacodemon
{
	translation "170:191=64:79", "16:47=144:151", "160:167=141:141", "223:223=143:143"
	+NoInfighting
	speed 16
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
	States
	{
		Missile:
			HEAD BC 5 A_FaceTarget
			HEAD D 0 Bright A_CustomMissile("CacodemonBall", 32, 0,-10)
			HEAD D 0 Bright A_CustomMissile("CacodemonBall", 32, 0,0)
			HEAD D 5 Bright A_CustomMissile("CacodemonBall", 32, 0,10)
			Goto See
	}
}

// Large Baron of Hell for Eclipse level
ACTOR BaronOfHell2 : BaronOfHell
{
	Radius 36
	Height 96
	Speed 16
	scale 1.5
	translation "48:79=180:191", "16:31=217:223", "32:47=232:235", "0:0=235:235"
	+NoInfighting
	+MISSILEMORE
	+MISSILEEVENMORE
	+LOOKALLAROUND
	Obituary "%o was sacrificed to the God Hand"
	States
	{
		Missile:
			BOSS EF 8 A_FaceTarget
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -20)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -15)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -10)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -5)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 0)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 5)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 10)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 15)
			BOSS G 8 A_CustomMissile ("BaronBall", 70, 0, 20)
			Goto See
	}
}

// Large Baron of Hell (Flight Wacky Mod)
ACTOR BaronOfHell2_Flight : BaronOfHell2
{
	+NOGRAVITY
	+FLOAT
}

// Large Baron of Hell (Rage Wacky Mod)
ACTOR BaronOfHell2_Rage : BaronOfHell2
{
	States
	{
		Missile:
			BOSS EF 4 A_FaceTarget
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -20)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -15)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -10)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, -5)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 0)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 5)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 10)
			BOSS G 0 A_CustomMissile ("BaronBall", 70, 0, 15)
			BOSS G 4 A_CustomMissile ("BaronBall", 70, 0, 20)
			Goto See
	}
}

// Large Baron of Hell (Spread Wacky Mod)
ACTOR BaronOfHell2_Spread : BaronOfHell2
{
	// No changes
}

// SkullTag Grenade launcher weapon
ACTOR GrenadeLauncher : DoomWeapon 5011
{
	Game Doom
	SpawnID 163
	Radius 20
	Height 16
	Weapon.Selectionorder 2500
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Weapon.Kickback 100
	Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER"
	States
	{
		Spawn:
			GRLG A -1
			stop
		Ready:
			GRLG A 1 A_WeaponReady
			loop
		Deselect:
			GRLG A 1 A_Lower
			loop
		Select:
			GRLG A 1 A_Raise
			loop
		Fire:
			GRLG B 8 A_GunFlash
			GRLG B 0 A_JumpIfInventory("BlueTeamItem", 1, "FireBlue")
			GRLG B 0 A_JumpIfInventory("RedTeamItem", 1, "FireRed")
			GRLG B 12 A_FireCustomMissile("DWGrenade", 0, 1, 0, 0, 0, 6.328125)
			GRLG B 0 A_ReFire
			goto Ready
		FireBlue:
			GRLG B 12 A_FireCustomMissile("DWGrenadeBlue", 0, 1, 0, 0, 0, 6.328125)
			GRLG B 0 A_ReFire
			goto Ready
		FireRed:
			GRLG B 12 A_FireCustomMissile("DWGrenadeRed", 0, 1, 0, 0, 0, 6.328125)
			GRLG B 0 A_ReFire
			goto Ready
		Flash:
			GRLF A	3 bright A_Light1
			GRLF B	4 bright
			GRLF C 4 bright A_Light2
			GRLF D 4 bright A_Light2
			Goto LightDone
	}
}

// Doomware Grenade
ACTOR DWGrenade
{
	Radius 8
	Height 8
	Speed 25
	Gravity 0.25
	Projectile
	-NoGravity
	+DoomBounce
	+DehExplosion
	+GrenadeTrail
	+NOEXTREMEDEATH
	SeeSound "Weapons/GrBnce"
	DeathSound "Weapons/GrenLX"
	Obituary "%o tripped over a grenade."
	States
	{
		Spawn:
			SGRN A 0 Bright ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			SGRN A 0 Bright A_StopSound
			SGRN A 0 Bright A_PlaySound("Weapons/GrenLF")
			SGRN A 1 Bright
			Goto Spawn+3
		Death:
			MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
			MISL B 8 Bright A_Explode(80)
			MISL C 0 Bright A_JumpIfInventory("WonRoundItem", 1, 1)
			MISL C 6 Bright
			MISL D 4 Bright
			stop
	}
}

// Doomware Grenade (Blue Team)
ACTOR DWGrenadeBlue : DWGrenade
{
	Translation "80:111=192:207", "5:8=240:247", "3:3=203:203"
}

// Doomware Grenade (Red Team)
ACTOR DWGrenadeRed : DWGrenade
{
	Translation "80:111=168:191", "5:8=191:191", "3:3=186:186"
}

// Big red target to shoot
ACTOR Target 11146
{
	Health 40000000
	Radius 16
	Height 32
	Mass 0x7fffffff
	painchance 256
	+FLOORCLIP
    +QUICKTORETALIATE
	+NoBlood
	+Solid
	+Shootable
	monster
	States
	{
		Spawn:
			TARG A 1
			Loop
		See:
			TARG A 3
			Loop
		Pain:
			TARG A 1 ACS_ExecuteAlways(10, 0)
			Goto see
		Death:
			TNT1 A 1 A_CustomMissile("Target")
			stop
	}
}

// Basketball entity
ACTOR BasketBall
{
	Radius 2
	Height 8
	Speed 10
	Gravity 0.25
	damage 0
	Projectile
	-NoGravity
	+HexenBounce
	+RIPPER
	+BLOODLESSIMPACT
	SeeSound "DoomWare/BBallBounce"
	DeathSound ""
	Obituary ""
	scale 0.5
	States
	{
		Spawn:
			BBAL A 0 A_JumpIfInventory("WonRoundItem",1,"Death")
			BBAL A 1 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			Goto Spawn
		Death:
			TNT1 A 3
			stop
	}
}

// Basketball Weapon
ACTOR BBallLauncher : DoomWeapon 11148
{
	Radius 16
	Height 32
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.AmmoType "RocketAmmo"
	Inventory.PickupMessage "$PICKUP_GRENADELAUNCHER"
	States
	{
		Spawn:
			BBAL B -1
			stop
		Ready:
			BBAL B 1 A_WeaponReady
			loop
		Deselect:
			BBAL B 1 A_Lower
			loop
		Select:
			BBAL B 1 A_Raise
			loop
		Fire:
			BBAL B 1 Offset (0, 33)
			BBAL B 1 Offset (0, 45)
			BBAL B 1 Offset (0, 47)
			BBAL B 1 Offset (0, 51)
			BBAL B 1 Offset (0, 55)
			BBAL B 1 Offset (0, 59)
			BBAL B 10 Offset (0, 63)
			BBAL B 1 Offset (0, 53)
			BBAL B 1 Offset (0, 43)
			BBAL B 10 Offset (0, 125) A_FireCustomMissile ( "BasketBall", 0, 1, 0, 0, 0, 6.328125 )
			BBAL B 1 Offset (0, 125) A_CheckReload
			BBAL B 1 Offset (0, 112)
			BBAL B 1 Offset (0, 80)
			BBAL B 1 Offset (0, 48)
			BBAL B 1
			BBAL B 1 A_ReFire
			goto Ready
		Flash:
			Goto LightDone
	}
}

// Bow and arrow weapon
ACTOR BowArrow : Weapon 11149
{
    Inventory.PickupMessage "You picked up the bow."
    Obituary "%o was perforated by %k's bow."
    Weapon.AmmoType1 "RocketAmmo"
    Weapon.AmmoGive1 20
    Weapon.AmmoUse1 1
	+NOAUTOAIM
	+DONTBOB
    Radius 30
	Height 10
    Scale .20
    States
    {
		Ready:
		   BOWA H 0
		   BOWA H 1 A_WeaponReady
		   Goto Ready+1
		Deselect:
		   BOWA H 1 A_Lower
			Loop
		Select:
		   BOWA H 0	
		   BOWA H 1 A_Raise
		   Goto Select+1
		Fire:
		   BOWA HIJKL 2
		   BOWA H 0
		   BOWA L 4 A_Playsound("DoomWare/BowDraw")
			Goto Hold
		Hold:
		   BOWA M 1
		   BOWA M 0 A_Refire
		   BOWA M 0 A_Playsound("DoomWare/BowFire")
		   BOWA O 0 A_FireCustomMissile("ArrowShot", 0, 0, 8, 8)
		   BOWA O 0 A_GunFlash
		   BOWA P 7
		   BOWA G 2
		   Goto Ready
		Flash:
		   TNT1 A 19
		   Stop
    }
}

// Arrow projectile from the bow
ACTOR ArrowShot
{
	Radius 3
	Height 2
	Speed 50
	Scale .5
	Damage 50
	PROJECTILE
	gravity 1
	+GHOST
	+WINDTHRUST
	+NOEXTREMEDEATH
	SeeSound "weapons/bowfire"
	DeathSound "weapons/bowhit"
	States
	{
	   Spawn:
		  ARRW AAAAAA 2 A_SpawnItem("BulletPuff")
		  Loop
	   Death:
		  ARRW A 0 A_JumpIfInventory("WonRoundItem",1,2)
		  ARRW A 1
		  Stop
		  ARRW A 1 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
		  Stop
	}
}

// Apple to shoot off an Imp's head
ACTOR Apple
{
	Health 40000
	Radius 16
	Height 32
	Mass 0x7fffffff
    painchance 256
	+FLOORCLIP
    +QUICKTORETALIATE
	+NoBlood
	+Solid
	+Shootable
	scale 0.75
	monster
	States
	{
		Spawn:
			APPL A 1 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
            APPL A 0 ACS_NamedExecuteAlways("DoomWare_Entity_CheckCleanup", 0)
			Loop
		See:
			APPL A 3 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
            APPL A 0 ACS_NamedExecuteAlways("DoomWare_Entity_CheckCleanup", 0)
			Loop
        Pain:
			APPL A 0 ACS_ExecuteAlways(10, 0)
			Goto See
		Death:
			TNT1 A 1
			stop
	}
}

// Imp that'll have the apple on his head
ACTOR ImpTarget
{
	Health 6000
	Radius 20
	Height 48
	Mass 0x7fffffff
	Speed 0
	painchance 256
	Monster
	+FLOORCLIP
    +ISMONSTER
    +QUICKTORETALIATE
	PainSound "imp/pain"
	DeathSound "imp/death"
	ActiveSound "imp/active"
	HitObituary "$OB_IMPHIT"
	Obituary "$OB_IMP"
	States
	{
		Spawn:
			TROO AB 10
			Loop
		See:
			TROO AABBCCDD 3
			Loop
		Pain:
            TROO A 0 ACS_ExecuteAlways(11, 0)
			TROO H 4 A_Pain
			Goto See
		Death:
			TNT1 A 1
			stop
	}
}

// Imp riding a Kart
ACTOR ImpKart
{
	Health 10
	Radius 16
	Height 30
	Mass 100
	+FLOORCLIP
	+NoBlood
	+Solid
	+Shootable
	monster
	States
	{
		Spawn:
			goto see
		See:
			KART B 2 A_Recoil(-4)
			KART B 0 A_PlaySound("DoomWare/KartEngine", 6, 1)
			Loop
		Death:
			KART A 0 A_GiveToTarget("WonRoundItem",1)
			MISL B 5 bright A_Playsound("weapons/rocklx")
			MISL C 5 bright
			MISL D 5 bright
			TNT2 A 5 bright
			TNT2 A -1
			Stop
	}
}

// Imp riding a Kart
ACTOR ImpKart1UP : ImpKart
{
	translation "64:79=112:127"
	States
	{
		Death:
			KART A 0 A_GiveToTarget("1UP", 1)
			KART A 0 A_GiveToTarget("WonRoundItem", 1)
			MISL B 5 bright A_Playsound("weapons/rocklx")
			MISL C 5 bright
			MISL D 5 bright
			TNT2 A 5 bright
			TNT2 A -1
			Stop
	}
}

// Pushing staff
ACTOR PushStaff : Fist
{
	Weapon.KickBack 2000
	obituary "%o was poked to death by %k"
	States
	{
		Ready:
			STFF A 1 A_WeaponReady
			Loop
		Deselect:
			STFF A 1 A_Lower
			Loop
		Select:
			STFF A 1 A_Raise
			Loop
		Fire:
			STFF B 6
			STFF C 8 A_FireBullets(0, 0, 1, 3, "MeleePuff", FBF_NOFLASH | FBF_NORANDOM, 64) // A_CustomPunch(20, true, CPF_NOTURN) // CPF_NOTURN Flag unsupported in Zandronum :/
			STFF B 8 A_ReFire
			Goto Ready
	}
}

// Bit trip player morph (Can only jump)
ACTOR JumpPlayer : DoomPlayer
{
    -NOSKIN
	Player.ForwardMove 0
	Player.SideMove 0
}

// Small fire effect for Ghoul's Forest level
ACTOR SmallFire 11150
{
	Radius 16
	Height 37
	-SOLID
	States
	{
		Spawn:
			SMFR ABCD 4 Bright
			Loop
	}
}

// Chilli flames
actor SmallFlames
{
    Damage 0
    Radius 6
    Height 8
	DamageType "Flame"
    Speed 18
    ExplosionDamage 20
    ExplosionRadius 38
    RENDERSTYLE ADD
    DONTHURTSHOOTER
    PROJECTILE
    - NOGRAVITY
	+DONTSPLASH
    ALPHA 0.9
	states
	{
		Spawn:
			  FRTB IHGFEDCBA 2 Bright
			  FRTB A 100 Bright
			  Goto Death
		Death:
			  FRTF A 0 A_PlaySound("vile/firecrkl")
			  FRTF ABCDEFG 5 Bright A_Explode
			  FRTF A 0 A_PlaySound("vile/firecrkl")
			  FRTF HIJKLMNO 5 Bright A_Explode
			  Stop
	}
}

// Pyoro bean
ACTOR Bean
{
	Radius 8
	Height 16
	Speed 0
	scale 2
	Damage 100
	Projectile
	States
	{
		Spawn:
			BEAN ABCB 4 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			Loop
		Death:
			TNT0 A 0 ACS_ExecuteAlways(245, 0)
			BEAN DEF 6
			TNT0 A 6
			Stop
	}
}

// Shark car
ACTOR DodgeCarA 11151
{
	SpawnID 254
	speed 64
	Projectile
	radius 32
	height 32
	Damage 100
	mass 100
	-NOGRAVITY
	+NOBLOCKMAP
	+RIPPER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	obituary "%o was attacked by a shark."
	States
	{
		Spawn:
            CARA A 0 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			CARA AB 2
			loop
		Death:
			TNT0 A 1
			stop
	}
}

// Weiner car
ACTOR DodgeCarB 11152
{
	SpawnID 253
	speed 64
	Projectile
	radius 32
	height 32
	Damage 100
	mass 100
	-NOGRAVITY
	+NOBLOCKMAP
	+RIPPER
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	obituary "%o died to a weiner."
	States
	{
		Spawn:
            CARB A 0 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			CARB ABCD 2
			loop
		Death:
			TNT0 A 1
			stop
	}
}

// Diglett (Whack a mole)
ACTOR Diglett : DoomImp
{
	Health 10
	Radius 16
	Height 32
	Mass 10000
	Speed 0
	PainChance 0
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	Monster
	+FLOORCLIP
	+NoBlood
	-SHOOTABLE
	States
	{
		Spawn:
			DIGL ONMLKJ 4 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			DIGL I 0 A_ChangeFlag("SHOOTABLE", true)
			DIGL A 0 A_PlaySound("DoomWare/DiglettSee")
			DIGL IED 4 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			goto See
		See:
			DIGL ABBBADDD 7 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			Loop
		Death:
			TNT0 A 0 A_GiveToTarget("WonRoundItem",1)
			TNT0 A 0 A_PlaySound("DoomWare/DiglettHurt")
			DIGL FFFGHBA 4
			DIGL D 0 A_PlaySound("DoomWare/DiglettDie")
			DIGL DEI 4
			DIGL I 0 A_ChangeFlag("SHOOTABLE", false)
			DIGL I 0 A_ChangeFlag("SOLID", false)
			DIGL JKLMNO 4
			TNT0 A 4
			Stop
	}
}

// Shiny diglett that gives 1-Up
ACTOR DiglettShiny : Diglett
{
	States
	{
		Spawn:
			DIGL ONMLKJ 4 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			DIGS I 0 A_ChangeFlag("SHOOTABLE", true)
			DIGS A 0 A_PlaySound("DoomWare/DiglettSee")
			DIGS IED 4 ACS_NamedExecuteAlways ("DoomWare_Entity_MarkCleanup", 0)
			goto See
		See:
			DIGS ABBBADDD 7 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			Loop
		Death:
			TNT0 A 0 A_GiveToTarget("1up",1)
			TNT0 A 0 A_GiveToTarget("WonRoundItem",1)
			TNT0 A 0 A_PlaySound("DoomWare/DiglettHurt")
			DIGS FFFGHBA 4
			DIGS I 0 A_PlaySound("DoomWare/DiglettDie")
			DIGS DEI 4
			DIGS I 0 A_ChangeFlag("SHOOTABLE", false)
			DIGS I 0 A_ChangeFlag("SOLID", false)
			DIGL JKLMNO 4
			TNT0 A 4
			Stop
	}
}

// DBZ aura
ACTOR Aura : Hat
{
	RenderStyle Add
	States
	{
		Spawn:
			TNT0 A 1
			AURA A 0 A_PlaySound("DoomWare/AuraStart")
			AURA ABCDECDECDE 4 Bright
			Goto See
		See:
			AURA A 0 A_PlaySound("DoomWare/AuraLoop")
			AURA CDECDECDE 3 Bright
			Loop
		Death:
			AURA A 0 A_PlaySound("DoomWare/AuraDie")
			AURA FGH 4 Bright
			Stop
	}
}

// Imma Firin' mah Laz0r (Used in DBZ round)
ACTOR Laz0r : DoomWeapon 11153
{
	Game Doom
	Radius 20
	Height 16
	Weapon.Selectionorder 100
	Obituary "%o was overcharged by %k"
	States
	{
		Ready:
			TNT2 A 6
			Goto Ready
		Deselect:
			TNT2 A 1 A_Lower
			loop
		Select:
			TNT2 A 0 A_PlaySound("DoomWare/Laz0r")
			TNT2 A 0 A_RailAttack(200, 0, false, "", "00 FF FF", RGF_SILENT | RGF_FULLBRIGHT , 0, "none", 0, 0, 1024, 35, 16.0, 0, "Laz0rTrail")
			TNT2 A 6
			Goto Ready
		Fire:
			Goto Ready
	}
}

// DBZ Laz0r trail
ACTOR Laz0rTrail : BulletPuff
{
	+ALWAYSPUFF
	+CLIENTSIDEONLY
	+BLOODLESSIMPACT
	+NOPAIN
	VSpeed 0
	Scale 0.5
	RenderStyle Add
	Alpha 0.5

	States
	{
		Spawn:
		ENGY A 0
		ENGY ABCDEFGHIJ 4 A_FadeTo(0, 0.1)
		Stop
	}
}

// DBZ dust effect
Actor Dust
{
	Projectile
	mass 100
	SpawnID 252
	-FLOORHUGGER
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	-SOLID
	-NOGRAVITY
	+NODAMAGETHRUST
	+NOEXTREMEDEATH
	States
	{
		Spawn:
			DUST ABCDEF 6 A_FadeTo(0, 0.17)
			Stop
	}
}

// Desert Bus player morph
ACTOR BusPlayer : DoomPlayer
{
	Player.ForwardMove 0
	Player.SideMove 0.5
	Player.JumpZ 0
	Health 100
	Radius 16
	Height 30
	Mass 100
	Player.ColorRange 112, 127
	PainSound ""
	DeathSound ""
	Player.MorphWeapon "BusWeapon"
	Player.SoundClass "Kart"
	+NOBLOOD
	+pushable
	+NOSKIN
	States
	{
		Missile:
			Goto See
		See:
			Spawn:
			BUSP A 5
			Loop
		Death:
			MISL B 5 bright A_Playsound("weapons/rocklx")
			MISL C 5 bright
			MISL D 5 bright
			TNT2 A 5 bright
			TNT2 A -1
			Stop
		XDeath:
			MISL B 5 bright A_Playsound("weapons/rocklx")
			MISL C 5 bright
			MISL D 5 bright
			TNT2 A 5 bright
			TNT2 A -1
			Stop
	}
}

// Desert Bus viewmodel weapon
ACTOR BusWeapon : DoomWeapon
{
	Weapon.SelectionOrder 2000
	+WEAPON.DONTBOB
	+WEAPON.ALT_AMMO_OPTIONAL
	Weapon.AmmoUse2   0
	States
	{
		Ready:
			BUSH A 0 A_WeaponReady
			BUSH A 0 A_PlaySound("DoomWare/BusLoop")
			BUSH A 70
			loop
		Deselect:
			BUSH A 0 A_StopSound(6)
			BUSH A 1 A_Lower
			loop
		Select:
			BUSH A 1 A_Raise
			Loop
		Fire:
			goto Ready
	}
}

// Time freeze powerup from Skulltag
ACTOR TimeFreeze : PowerupGiver
{
	Inventory.Pickupmessage "Time Freezer!"
	Inventory.Icon "MEGAA0"
	Inventory.MaxAmount 0
	Inventory.UseSound "pickups/slowmo"
	Powerup.Type "TimeFreezer"
	Powerup.Duration 350
	Translation "128:143=48:63"
	+AUTOACTIVATE
	+INVENTORY.FANCYPICKUPSOUND
	States
	{
	Spawn:
		MEGA ABCD 4 bright
		Loop
	}
}

// Shadow of the Colossus rain emitter
ACTOR RainSpawner 11154
{
	+Missile
	+NoClip
	+NoGravity
	+NoBlockMap
	+SPAWNCEILING
	RenderStyle Translucent
	Alpha 0
    
 	states
 	{
		Spawn:
			TNT1 A 1
			loop
        Activated:
            TNT1 A 0 A_SpawnItemEx ("RainDrop", random(-512,-512), random(-512,-512), 0, random(0,1), random(0,1), -random(20,150), random(0,255), 0)
            TNT1 A 1 A_SpawnItemEx ("RainDrop", random(-512,-512), random(-512,-512), 0, random(0,1), random(0,1), -random(20,150), random(0,255), 0)  
			loop
		Deactivated:
			TNT1 A 1
			loop
	}

}

// Shadow of the Colossus rain drop with splat
ACTOR RainDrop
{
    radius 1
	+CLIENTSIDEONLY
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+NOTELEPORT
	+DontSplash
	projectile
	RenderStyle Add
	Alpha .5
	scale 0.75
    
	states
	{
        Spawn:
            RAIN A 1 bright
            Loop
        Death:
            RAIN B 0 A_NoGravity
            RAIN B 0 A_Stop
            RAIN BCDE 3
            Stop
	}
}

// Shadow of the Colossus rain mist spawner
ACTOR RainMistSpawner 11155
{
	+Missile
	+NoClip
	+NoGravity
	+NoBlockMap
	+SPAWNCEILING
	RenderStyle Translucent
	Alpha 0
    
 	states
 	{
		Spawn:
			TNT1 A 1
			loop
        Activated:
            TNT1 A 10 A_SpawnItemEx ("RainMist", 0, 0, 0, random(0,1), random(0,1), -random(10,60), random(0,255), 0) 
            loop
		Deactivated:
			TNT1 A 1
			loop
	}

}

// Shadow of the Colossus rain mist particle
ACTOR RainMist
{
	+CLIENTSIDEONLY
	+NOBLOCKMAP 
	+DROPOFF 
	+MISSILE 
	+NOTELEPORT
	+DontSplash
    RenderStyle ADD
	Alpha .05
	scale 4
    projectile
    
	states
	{
        Spawn:
            RMST A 175 
            Stop
        Death:
            TNT1 A 1
            Stop
	}

}

// Halloween mode death ghost
ACTOR SpoopyGhost
{
	VSpeed 3
	scale 0.5
	RenderStyle Translucent
	+NoGravity
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			GOST A 1 A_SetScale(0.5, 1.0)
			GOST A 2 A_playsound("DoomWare/SpoopyGhost")
			GOST A 0 A_SetScale(0.5, 1.05)
			GOST A 1 A_FadeOut(0.05)
            GOST A 0 A_SetScale(0.5, 1.1)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.15)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.2)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.25)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.3)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.35)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.4)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.45)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.5)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 2.55)
			GOST A 1 A_FadeOut(0.05)
            GOST A 0 A_SetScale(0.5, 1.6)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.65)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.7)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.75)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.8)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.85)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.9)
			GOST A 1 A_FadeOut(0.05)
			GOST A 0 A_SetScale(0.5, 1.95)
            GOST A 1 A_FadeOut(0.05)
			Stop
	}
}

// Orange halloween torch
ACTOR OrangeTorch
{
	Radius 16
	Height 68
	ProjectilePassHeight -16
	+SOLID
	States
	{
		Spawn:
			TORG ABCD 4 Bright
			Loop
	}
}

// White Christmas torch
ACTOR WhiteTorch
{
	Radius 16
	Height 68
	ProjectilePassHeight -16
	+SOLID
	States
	{
		Spawn:
			TORW ABCD 4 Bright
			Loop
	}
}

// Orange OpenGL light emitter
ACTOR OrangePoint
{
	+NOBLOCKMAP
	+NOSECTOR
	+NOGRAVITY
	+DONTSPLASH
	+MISSILEMORE
	RenderStyle None
	CameraHeight 0
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Off:
			TNT2 A 1
			Loop
	}
}

// Blue OpenGL light emitter
ACTOR BluePoint
{
	+NOBLOCKMAP
	+NOSECTOR
	+NOGRAVITY
	+DONTSPLASH
	+MISSILEMORE
	RenderStyle None
	CameraHeight 0
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Off:
			TNT2 A 1
			Loop
	}
}

// Red OpenGL light emitter
ACTOR RedPoint
{
	+NOBLOCKMAP
	+NOSECTOR
	+NOGRAVITY
	+DONTSPLASH
	+MISSILEMORE
	RenderStyle None
	CameraHeight 0
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Off:
			TNT2 A 1
			Loop
	}
}

// White OpenGL light emitter
ACTOR WhitePoint
{
	+NOBLOCKMAP
	+NOSECTOR
	+NOGRAVITY
	+DONTSPLASH
	+MISSILEMORE
	RenderStyle None
	CameraHeight 0
	States
	{
		Spawn:
			TNT1 A 1
			Loop
		Off:
			TNT2 A 1
			Loop
	}
}

// Buu342 static actor for Intro
ACTOR BuuIntro 11156
{
	Health 40
	Radius 10
	Height 42
	Mass 100
	Speed 0
	Scale 0.25
	PainChance 0
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	Monster
	+FLOORCLIP
	+NoBlood
	States
	{
		Spawn:
			BUDW A 1
			Loop
		Dance:
			BUDW H 1
			Loop
	}
}

// Buu342 dance star effect emitter
ACTOR StarSparkleEmitter
{
	States
	{
		Spawn:
			TNT1 B 1
			TNT1 B 0 A_PlaySound("DoomWare/StarSparkle", CHAN_AUTO, 0.5)
			TNT1 BBBBBBBB 0 A_SpawnItemEx("StarEffect", 8+Random(-14,14), Random(-14,14), 42+Random(-40,-10), random(1,2), random(1,2), 8, random(-180,180), 0, 16)
			TNT1 B 1
			Stop
	}
}

// Buu342 dance star effect particle
Actor StarEffect
{
	+CLIENTSIDEONLY
	-NOGRAVITY
	RenderStyle Add
	Gravity 0.7
	Alpha 0.5
	States
	{
		Spawn:
			STAR AAAAAAAAAAA 2 Bright A_SpawnItemEx("StarFadeEffect")
			Stop
	}
}

// Buu342 dance star effect ghosts
Actor StarFadeEffect
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Add
	Alpha 0.25
	States
	{
		Spawn:
			STAR A 1 Bright A_FadeOut(0.02)
			Loop
	}
}

// Fake Buu342 player
ACTOR Buu342Phase1
{
	Health 100
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	mass 100
	Scale 0.22
	Monster
	-SOLID
	+FLOORCLIP
	+LOOKALLAROUND
	+MISSILEMORE
	+MISSILEEVENMORE
	PainSound "DoomWare/BuuPant"
	AttackSound "shotguy/attack"
	DeathSound "DoomWare/BuuYeah"
	SeeSound "DoomWare/BuuAwwYeah"
	Obituary "%o didn't stand a chance against \cqB\csu\cdu\cq3\cs4\cd2\c-"
	States
	{
		Spawn:
			BUDW A 10 A_Look
			Loop
		See:
			BUDW AAAA 1 A_Chase("", "Missile")
			BUDW A 0 A_Recoil(-2)
			BUDW BBBB 1 A_Chase("", "Missile")
			BUDW B 0 A_Recoil(-2)
			BUDW CCCC 1 A_Chase("", "Missile")
			BUDW C 0 A_Recoil(-2)
			BUDW DDDD 1 A_Chase("", "Missile")
			BUDW D 0 A_Recoil(-2)
			BUDW D 0 A_Jump(128, "See")
			BUDW D 0 ThrustThingZ(0, 35, 0, 1)
			Loop
		Missile:
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 A_FaceTarget
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 A_FaceTarget
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 A_FaceTarget
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 A_FaceTarget
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 A_FaceTarget
			BUDW F 8 A_SPosAttackUseAtkSound
			BUDW F 0 A_Jump(128, 2)
			BUDW F 0 ThrustThingZ(0,35,0,1)
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 
			BUDW E 0 A_Recoil(-2)
			BUDW E 2 
			Goto See
		Pain:
			BUDW G 3
			BUDW G 3 A_Pain
			Goto See
		Death:
			BUDW I 5 
			BUDW J 5 A_Scream
			BUDW K 5 A_NoBlocking
			BUDW L 5
			BUDW M -1
			Stop
	}
}

// Buu342's head during phase 1 to 2 transition
ACTOR Buu342HeadCorpse
{
	+NOGRAVITY
	scale 0.05
	States
	{
		Spawn:
			BUHD A 1 ThrustThingZ(0,3,0,0)
			Loop
		Paused:
			BUHD A 1 ThrustThingZ(0,0,0,0)
			BUHD A 1 A_ChangeFlag("FLOATBOB", true)
			Loop
	}
}

// Buu342 phase 2
ACTOR Buu342Phase2 11157
{
	Monster
	health 66666
	radius 128
	height 200
	mass 10000
	painchance 2
	MinMissileChance 220
	-SHOOTABLE
	+FLOATBOB
	+NOGRAVITY
	+BOSS
	+SOLID
	+FLOAT
	+DONTFALL
	States
	{
		Spawn:
			BUHD A 105
			BUHD A 0 A_PlaySound("DoomWare/Laugh", 0, 1, false, ATTN_NONE)
			BUHD BABABABABABABABABABABBBABBAAABBBBBAAA 5
			BUHD A 0 A_ChangeFlag("SHOOTABLE", true)
			Goto See
		See:
			BUHD AAAA 4 A_Chase("", "Missile", CHF_DONTMOVE)
			Loop
		Missile:
			BUHD A 0 A_Jump(20, "Missile2") // Grenades
			BUHD A 0 A_Jump(20, "Missile3") // Sky Laser
			BUHD A 0 A_Jump(20, "Missile4") // Bouncy Balls
			BUHD A 0 A_Jump(20, "Missile5") // Archvile attack
			BUHD A 0 A_Jump(20, "Missile6") // Flame Breath
			BUHD A 0 A_Jump(28, "Missile7") // Eye Laser
			BUHD BB 2 A_FaceTarget
			BUHD B 0 A_PlaySound("Lilith/Attack", 0, 1, false, ATTN_NONE)
			BUHD B 0 A_CustomMissile("LilithBall1", 60, 0, -5)  
			BUHD B 0 A_CustomMissile("LilithBall1", 60, 0, 0) 
			BUHD B 0 A_CustomMissile("LilithBall1", 60, 0, 5)
			BUHD B 0 A_ClearTarget
			Goto See 
		Missile2:
			BUHD BB 2 A_FaceTarget
			BUHD B 0 A_PlaySound("Lilith/Attack", 0, 1, false, ATTN_NONE)
			BUHD B 0 A_CustomMissile("DWGrenade", 60, 0, -5) 
			BUHD B 0 A_CustomMissile("DWGrenade", 60, 0, -10) 
			BUHD B 0 A_CustomMissile("DWGrenade", 60, 0, 0)
			BUHD B 0 A_CustomMissile("DWGrenade", 60, 0, 5)
			BUHD B 0 A_CustomMissile("DWGrenade", 60, 0, 10)
			BUHD BBB 10 A_FaceTarget
			BUHD B 0 A_ClearTarget
			Goto See 
		Missile3:
			BUHD C 0 ACS_Execute(666, 0)
			BUHD BFBFBF 5 A_FaceTarget
			BUHD F 35 A_FaceTarget
			BUHD B 0 A_ClearTarget
			Goto ChangePos
		Missile4:
			BUHD A 0 A_PlaySound("Lilith/Attack", 0, 1, false, ATTN_NONE)
			BUHD A 1 A_CustomMissile("Buu342BallSpawner",60,0,315)
			BUHD B 1 A_CustomMissile("Buu342BallSpawner",60,0,326)
			BUHD A 1 A_CustomMissile("Buu342BallSpawner",60,0,337)
			BUHD B 1 A_CustomMissile("Buu342BallSpawner",60,0,348)
			BUHD A 1 A_CustomMissile("Buu342BallSpawner",60,0,359)
			BUHD B 1 A_CustomMissile("Buu342BallSpawner",60,0,11)
			BUHD A 1 A_CustomMissile("Buu342BallSpawner",60,0,22)
			BUHD B 1 A_CustomMissile("Buu342BallSpawner",60,0,33)
			Goto See
		Missile5:
			BUHD A 0 A_VileStart 
			BUHD A 0 ACS_ExecuteAlways(668, 0)
			BUHD A 5 A_FaceTarget
			BUHD E 8 A_VileTarget
			BUHD EEEEEE 8 A_FaceTarget
			BUHD B 8 A_VileAttack
			BUHD B 10 
			BUHD B 0 A_ClearTarget
			Goto ChangePos
		Missile6:
			BUHD B 0 A_PlaySound("DoomWare/BuuFlameBreath", 0, 1, false, ATTN_NONE)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, -5)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, -4)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, -3)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, -2)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, -1)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 0)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 1)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 2)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 3)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 4)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 5)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 4)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 3)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 2)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 1)
			BUHD B 4 A_CustomMissile("FlameBreath", 50, 0, 0)
			BUHD B 0 A_ClearTarget
			Goto See
		Missile7:
			BUHD F 2 A_FaceTarget
			BUHD F 0 A_PlaySound("DoomWare/BuuEyeLaser", 0, 1, false, ATTN_NONE)
            BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -18)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -18) 
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -18, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -18, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -16)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -16)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -16, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -16, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -14)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -14)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -14, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -14, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -12)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -12)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -12, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -12, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -10)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -10)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -10, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -10, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -8)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -8)
			BUHD F 0 A_PlaySound("DoomWare/BuuEyeLaserExplosion", 0, 1, false, ATTN_NONE)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -8, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -8, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -6)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -6)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -6, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -6, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -4)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -4)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -4, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -4, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, -2)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, -2)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -2, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, -2, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, 0)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, 0)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, 0, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, 0, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, -50, 0, CMF_AIMDIRECTION, 2)
			BUHD F 0 A_CustomMissile("DelayedExplosion", 20, 50, 0, CMF_AIMDIRECTION, 2)
            BUHD F 0 A_CustomRailgun(0, -50, None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, 2, 0, 2, 1.0, 0.0, "None", 20)
			BUHD F 2 A_CustomRailgun(0, 50,  None, "Red", RGF_SILENT|RGF_EXPLICITANGLE|RGF_FULLBRIGHT, 0, 0, None, 0, 2, 0, 2, 1.0, 0.0, "None", 20)
			BUHD B 0 A_ClearTarget
			Goto ChangePos
		Pain:
			BUHD D 0 A_PlaySound("DoomWare/BuuPant", 0, 1, false, ATTN_NONE)
			BUHD D 0 A_ChangeFlag("SOLID", false)
			BUHD D 0 ACS_Execute(667, 0)
			BUHD DDDDDDD 5 A_SpawnItemEx("Buu342Phase2Ghost")
			BUHD D 0 A_ChangeFlag("SOLID", true)
			Goto See
		ChangePos:
			BUHD A 0 A_ChangeFlag("SOLID", false)
			BUHD A 0 ACS_Execute(667, 0)
			BUHD AAAAAAA 5 A_SpawnItemEx("Buu342Phase2Ghost2")
			BUHD A 0 A_ChangeFlag("SOLID", true)
			Goto See
		TurnInvisible:
			BUHD D 0 A_PlaySound("DoomWare/BuuInvisible", 0, 1, false, ATTN_NONE)
			BUHD A 0 A_ChangeFlag("SOLID", false)
			BUHD D 0 A_ChangeFlag("SHOOTABLE", false)
			BUHD D 1 A_SetTranslucent(0.8)
			BUHD D 1 A_SetTranslucent(0.6)
			BUHD D 1 A_SetTranslucent(0.4)
			BUHD D 1 A_SetTranslucent(0.2)
			BUHD D 1 A_SetTranslucent(0)
		Paused:
			BUHD A 10
			Loop
		TurnVisible:
			BUHD D 0 A_PlaySound("DoomWare/BuuInvisible", 0, 1, false, ATTN_NONE)
			BUHD D 1 A_SetTranslucent(0.2)
			BUHD D 1 A_SetTranslucent(0.4)
			BUHD D 1 A_SetTranslucent(0.6)
			BUHD D 1 A_SetTranslucent(0.8)
			BUHD D 1 A_SetTranslucent(1)
			BUHD A 0 A_ChangeFlag("SHOOTABLE", true)
			BUHD A 0 A_ChangeFlag("SOLID", true)
			Goto See
		Laugh:
			BUHD A 0 A_PlaySound("DoomWare/Laugh", 0, 1, false, ATTN_NONE)
			BUHD BABABABABABABABABABABBBABBAAABBBBBAAA 5
			Goto Paused
		Death:
			BUHD D 0 A_SetTranslucent(1)
			BUHD G 1
			BUHD G 1 A_PlaySound("DoomWare/BuuRoar", 0, 1, false, ATTN_NONE)
			BUHD G 245
			BUHD HIHIHIHIHIHIHIHIHIHIHIHIHIHIHHIHIHIHIHIHIHIHIHIHIHIHIHIHIH 2
			BUHD HIHIHIHIHIHIHIHIHIHIHIHIHIHIHHIHIHIHIHIHIHIHIHIHIHIHIHIHIH 2 A_CustomMissile("Explosion_Effect", random(0, 200), random(-128, 128), random(0, 360))
			BUHD G 20
			BUHD G 0 A_PlaySound("DoomWare/BuuNoProblem", 0, 1, false, ATTN_NONE)
			BUHD G 60
			BUHD G 0 A_SetMass(10)
			BUHD G 0 A_Gravity
			BUHD G 0 A_PlaySound("DoomWare/BigBoom", 0, 1, false, ATTN_NONE)
			BUHD JJJJJ 2 A_CustomMissile("Explosion_Effect", random(0, 200), random(-128, 128))
			BUHD KKKKK 2 A_CustomMissile("Explosion_Effect", random(0, 200), random(-128, 128))
			BUHD LLLLL 2 A_CustomMissile("Explosion_Effect", random(0, 200), random(-128, 128))
			TNT1 A 1
			Stop
	}
}

// Buu342 sky laser
Actor Buu342Laser
{
	Health 10000000000
	Radius 16
	Height 93
	Speed 0
	Damage 0
	Alpha 0.9
	Mass 9999999999
	Obituary "%o was disintegrated."
	renderstyle add
	Translation "192:207=112:127"
	Monster
	- Solid
	+ shootable
	+ Noblood
	- NOBLOCKMAP
	+ NoTarget
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_PlaySound("DoomWare/BuuLaser1")
			LASR ABABABABABAB 3 Bright
			LASR C 2 Bright A_NoBlocking
			LASR C 0 A_PlaySound("DoomWare/BuuLaser2")
			LASR D 2 Bright A_Explode(500,64)
			LASR DDDDDDDDDDDDDDDDDD 0 A_CustomMissile("FloorProtrude",0,0,random(0,360),2,random(-20,90))
			LASR E 2 Bright A_Explode(500,64)
			LASR F 0 A_Explode(500,64)
			LASR G 2 Bright A_ChangeFlag("SOLID", 1)
			LASR H 2 Bright
			LASR I 2 Bright
			LASR J 2 Bright
			LASR K 2 Bright
			LASR L 2 Bright
			LASR M 2 Bright
			Stop
	}
}

// Buu342 moving ghost with eyes closed
ACTOR Buu342Phase2Ghost 
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle Add
	Alpha 1
	States
	{
		Spawn:
			BUHD D 1 Bright A_FadeOut(0.1)
			Loop
	}
}

// Buu342 moving ghost with eyes open
ACTOR Buu342Phase2Ghost2 : Buu342Phase2Ghost
{
	States
	{
		Spawn:
			BUHD A 1 Bright A_FadeOut(0.1)
			Loop
	}
}

// Buu342 bouncing ball spawner
ACTOR Buu342BallSpawner
{
	speed 32
	Radius 5
	height 5
	projectile
	Alpha 0.9
	- ACTIVATEIMPACT
	- ACTIVATEPCROSS
	- NOGRAVITY
	+ FULLVOLDEATH
	+ CANBOUNCEWATER
	+ NOWALLBOUNCESND
	+ SEEKERMISSILE
	BounceFactor 1
	Obituary "%o was disintegrated."
	WallBounceFactor 1
	Translation "193:207=250:254","176:191=112:127","169:175=112:114","16:31=112:120","32:47=120:127","232:235=121:124","215:225=112:122","160:167=112:122"
	States
	{
		spawn:
			SBS1 ABCD 3 Bright A_Tracer
			Loop
		Death:
			TNT1 A 1 Bright A_CustomMissile("Buu342BallBouncer", 0, 0, 180)
			Stop
	}
}

// Buu342 bouncing balls
ACTOR Buu342BallBouncer 
{
	speed 7
	Radius 5
	height 5
	projectile
	Alpha 0.9
	- ACTIVATEIMPACT
	- ACTIVATEPCROSS
	- NOGRAVITY
	+ FULLVOLDEATH
	+ CANBOUNCEWATER
	+ NOWALLBOUNCESND
	+ SEEKERMISSILE
	+ HEXENBOUNCE
	BounceFactor 1
	Obituary "%o was disintegrated."
	WallBounceFactor 1
	Translation "193:207=250:254","176:191=112:127","169:175=112:114","16:31=112:120","32:47=120:127","232:235=121:124","215:225=112:122","160:167=112:122"
	States
	{
		spawn:
			SBS1 A 0 Bright
			SBS1 D 1 Bright ThrustThingZ(0, 32, 0, 0)
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			SBS1 ABCD 3 Bright A_Tracer
			Goto Death
		Death:
			HECF C 2 Bright A_Explode(30, 128)
			HECF DE 6 Bright
			Stop
	}
}

// Delayed explosion for Buu342's eye lasers
ACTOR DelayedExplosion : DoomImpBall
{
	Damage 0
	Speed 128
    -RANDOMIZE
	+BLOODLESSIMPACT
	+RIPPER
	RenderStyle Add
	Alpha 1
	SeeSound ""
	DeathSound ""
	States
	{
		Spawn:
			TNT1 A 1 
			Loop
		Death:
			TNT1 A 10 
			BAL1 CD 6 Bright
			BAL1 E 0 A_Explode(30, 64, 0, false, 64)
			BAL1 E 6 Bright
			Stop
	}
}

// Buu342 flame breath
ACTOR FlameBreath : SmallFlames
{
    Speed 32
	+NOGRAVITY
}

// BulletPuff for boss round
ACTOR CoopBulletPuff : BulletPuff
{
	DamageType "CoopBullet"
}

// Shotgun for boss round
ACTOR CoopShotgun : Shotgun
{
	States
	{
		Fire:
			SHTG A 3
			SHTG A 0 A_FireBullets(5.6, 0, 7, 5, "CoopBulletPuff")
			SHTG A 0 A_PlaySound("weapons/shotgf", CHAN_WEAPON)
			SHTG A 7 A_GunFlash
			SHTG BC 5
			SHTG D 4
			SHTG CB 5
			SHTG A 3
			SHTG A 7 A_ReFire
			Goto Ready
	}
}

// Chaingun for boss round
ACTOR CoopChaingun : Chaingun
{
	States
	{
		Fire:
			CHGG A 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
			CHGG A 0 A_GunFlash
			CHGG A 4 A_FireBullets(5.6, 0, 1, 5, "CoopBulletPuff")
			CHGG B 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
			CHGG B 0 A_GunFlash("Flash2")
			CHGG B 4 A_FireBullets(5.6, 0, 1, 5, "CoopBulletPuff")
			CHGG B 0 A_ReFire
			Goto Ready
		Flash:
			CHGF A 4 Bright A_Light1
			Goto LightDone
		Flash2:
			CHGF B 4 Bright A_Light2
			Goto LightDone
	}
}

// Double damage for boss battle
actor DoubleDamagePickup : PowerDamage
{
	damagefactor "CoopBullet", 2
	Powerup.Duration 0x7FFFFFFF
}

// Quad damage for boss battle
actor QuadDamagePickup : PowerDamage
{
	damagefactor "CoopBullet", 4
	Powerup.Duration 0x7FFFFFFF
}

// Cyberdemon, but doesn't infight
actor Cyberdemon2 : Cyberdemon replaces Cyberdemon
{
	+NOINFIGHTING
}

// Flight Cyberdemon
actor Cyberdemon_Flight : Cyberdemon2
{
	+NOGRAVITY
	+FLOAT
}

// Spread Cyberdemon
actor Cyberdemon_Spread : Cyberdemon2
{
	States
	{
		Missile:
			CYBR E 6 A_FaceTarget
			CYBR F 0 A_PlaySound("weapons/rocklf")
			CYBR F 0 A_CustomMissile("Rocket", 40, 0, -10)  
			CYBR F 0 A_CustomMissile("Rocket", 40, 0, 0) 
			CYBR F 12 A_CustomMissile("Rocket", 40, 0, 10) 
			CYBR E 12 A_FaceTarget
			CYBR F 0 A_PlaySound("weapons/rocklf")
			CYBR F 0 A_CustomMissile("Rocket", 40, 0, -10)  
			CYBR F 0 A_CustomMissile("Rocket", 40, 0, 0) 
			CYBR F 12 A_CustomMissile("Rocket", 40, 0, 10) 
			CYBR E 12 A_FaceTarget
			CYBR F 0 A_PlaySound("weapons/rocklf")
			CYBR F 0 A_CustomMissile("Rocket", 40, 0, -10)  
			CYBR F 0 A_CustomMissile("Rocket", 40, 0, 0) 
			CYBR F 12 A_CustomMissile("Rocket", 40, 0, 10)
			Goto See
	}
}

// Rage Cyberdemon
actor Cyberdemon_Rage : Cyberdemon2
{
	States
	{
		Missile:
			CYBR E 3 A_FaceTarget
			CYBR F 6 A_CyberAttack
			CYBR E 6 A_FaceTarget
			CYBR F 6 A_CyberAttack
			CYBR E 6 A_FaceTarget
			CYBR F 6 A_CyberAttack
			Goto See
	}
}

// Captain Falcon player morph
ACTOR CFalconPlayer : DoomPlayer
{
	Player.SoundClass "cfalcon"
	Player.MorphWeapon "FalconPunchWeapon"
	Player.JumpZ 0
	Player.ColorRange 112, 127
	Health 100
	Scale 0.5
	+NOBLOOD
	+NOSKIN
	States
	{
		Spawn:
			CFAL A -1
			Loop
		See:
			CFAL ABCD 4
			Loop
		Missile:
			CFAL E 0 A_PlaySound("DoomWare/FalconPunch")
			CFAL EFG 10 A_SpawnItem("FPunchChargeEmitter")
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL K 10
			Goto Spawn
		Melee:
			Goto Missile
		Pain:
			Goto XDeath
		Death:
			Goto XDeath
		XDeath:
			TNT1 A 0 SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN)
			TNT1 A 0 A_FaceTarget 
			TNT1 A 0 A_SpawnItem("CaptainFalconDeath", 0, 0, false, true)
			TNT1 A 0 A_ChangeVelocity(-20, 0, 23, CVF_RELATIVE|CVF_REPLACE)
			TNT1 A 0 A_XScream
			TNT1 A 10 A_NoBlocking
			TNT1 A -1
			Stop
	}
}

// Captain Falcon player morph (Rage Rune)
ACTOR CFalconPlayerRage : CFalconPlayer
{
	States
	{
		Missile:
			CFAL E 0 A_PlaySound("DoomWare/FalconPunch")
			CFAL EFG 5 A_SpawnItem("FPunchChargeEmitter")
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL H 0 A_SpawnItemEx("FPunchFlamelet", random(-8, 8), random(-8, 8), 32+random(-4, 4), 10, 0, random(-3, 3), 180)
			CFAL HIJ 1 BRIGHT
			CFAL K 5
			Goto Spawn
	}
}

// Falcon Punch Charging Green Fire Effect
ACTOR FPunchCharge
{
	radius 8
	height 8
	projectile
	damage 0
	RenderStyle TRANSLUCENT
	+THRUACTORS
	+CLIENTSIDEONLY
	+NOGRAVITY
	-SOLID
	States
	{
		Spawn:
			FPCH A 1 BRIGHT A_SetScale(0.5, 0.1)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.2)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.3)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.4)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.5)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.6)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.7)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.8)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			FPCH A 0 BRIGHT A_SetScale(0.5, 0.9)
			FPCH A 1 BRIGHT A_FadeOut(0.1)
			Stop
	}
}

// Falcon Punch little flame particle
ACTOR FPunchFlamelet
{
	radius 8
	height 8
	projectile
	damage 0
	Scale 0.3
	RenderStyle TRANSLUCENT
	+THRUACTORS
	+CLIENTSIDEONLY
	+NOGRAVITY
	-SOLID
	States
	{
		Spawn:
			FPFR ABCDEF 2 BRIGHT
			Stop
	}
}

// Creates a circle of the above effect
ACTOR FPunchChargeEmitter
{
	+NOGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	-SOLID
	States
	{
		Spawn:
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -158, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -135, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -112, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -90, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -69, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -45, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, -22, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 0, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 22, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 45, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 69, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 90, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 112, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 135, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 158, SXF_CLIENTSIDE)
			TNT1 A 0 A_SpawnItemEx("FPunchCharge", 0, 0, 0, 6, 0, 0, 180, SXF_CLIENTSIDE)
			TNT1 A 1
			Stop
	}
}

// Falcon Punch morph weapon
ACTOR FalconPunchWeapon : DoomWeapon
{
	Weapon.SelectionOrder 2000
	+WEAPON.DONTBOB
	+WEAPON.ALT_AMMO_OPTIONAL
	Weapon.AmmoUse2   0
	obituary "%o was Falcon Punched by %k"
	States
	{
		Ready:
			TNT1 A 1 A_WeaponReady
			TNT1 A 0 A_StopSound(6)
			Loop
		Deselect:
			TNT1 A 0 A_StopSound(6)
			TNT1 A 1 A_Lower
			Loop
		Select:
			TNT1 A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 SetPlayerProperty(0, 1, PROP_TOTALLYFROZEN)
			TNT1 A 26
			CFST A 0 Offset(0, 32)
			CFST A 1 Offset(77, 91)
			CFST A 1 Offset(39, 62)
			CFST A 1 Offset(48, 68)
			CFST A 1 Offset(18, 44)
			CFST A 0 A_SpawnItemEx("Dust", 0, 0, 0, 5, 0, 0, 180)
			CFST A 0 A_FireCustomMissile("FalconPunchHitbox")
			CFST A 0 A_Recoil(-7)
			CFST A 0 A_PlaySound("DoomWare/PunchBoom", CHAN_WEAPON)
			CFST A 1 Offset(9, 36)
			CFST A 1 Offset(1, 27)
			CFST A 1 Offset(-8, 19)
			CFST A 1 Offset(-16, 10)
			CFST A 1 Offset(-25, 2)
			CFST A 1 Offset(-26, 2)
			CFST A 1 Offset(-27, 1)
			CFST A 1 Offset(-28, 1)
			CFST A 1 Offset(-29, 0)
			CFST A 1 Offset(-30, 0)
			CFST A 1 Offset(-31, 0)
			CFST A 2 Offset(-32, 0)
			CFST A 1 Offset(-33, 0)
			CFST A 1 Offset(-34, 0)
			CFST A 1 Offset(-42, 7)
			CFST A 1 Offset(-50, 13)
			CFST A 1 Offset(-59, 20)
			CFST A 1 Offset(-67, 26)
			CFST A 1 Offset(-75, 33)
			CFST A 1 Offset(-83, 42)
			CFST A 1 Offset(-90, 51)
			CFST A 1 Offset(-98, 61)
			CFST A 1 Offset(-105, 70)
			CFST A 1 Offset(-113, 79)
			CFST A 0 SetPlayerProperty(0, 0, PROP_TOTALLYFROZEN)
			CFST A 0 A_ReFire
			Goto Ready
	}
}

// Falcon Punch hitbox
ACTOR FalconPunchHitbox
{
	Radius 12
	Height 8
	Speed 4
	Damage 169
	Projectile 
	obituary "%o was Falcon Punched by %k"
	+RIPPER
	States
	{
		Spawn:
			TNT1 A 18
			Stop
	}
}

// Smash Bros Hit Effect
ACTOR SmashEffect
{
	Radius 32
	Height 64
	RenderStyle TRANSLUCENT
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	-SOLID
	States
	{
		Spawn:
			SMSH ABCDEFGHIJ 1 BRIGHT A_FadeTo(0, 0.1)
			TNT1 A 0
			Stop
	}
}

// Captain Falcon death effect
ACTOR CaptainFalconDeath
{
	Radius 12
	Height 8
	Scale 0.5
	projectile
	-MISSILE
	+NOBLOCKMAP
	-SHOOTABLE
	-PUSHABLE
	-BLOCKEDBYSOLIDACTORS
	-DONTTRANSLATE
	-NOGRAVITY
	+NOBLOCKMONST
	States
	{
		Spawn:
			CFAL L 1 
			CFAL L 0 A_SpawnItem("SmashEffect", 0, 32)
			CFAL L 1 A_ChangeVelocity(-20, 0, 23, CVF_RELATIVE|CVF_REPLACE)
			CFAL L 0 A_PlaySound("DoomWare/SSBHit", CHAN_BODY)
			CFAL L 0 A_PlaySound("DoomWare/CrowdOhh", CHAN_ITEM, 1, false, ATTN_NONE)
			CFAL LLLLLLLLLLLLLL 1 A_SpawnItem("SmokeTrail", 0, 16)
			CFAL MMMMMMMMMMMMMMMMMMMMMMMMMMM 1 A_SpawnItem("SmokeTrail", 0, 16)
			KOSM A 0 A_SetScale(5, 5)
			KOSM A 0 A_SetTranslucent(0.5, 1)
			KOSM A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE)
			KOSM A 0 A_PlaySound("DoomWare/SSBKO", CHAN_BODY, 1, false, ATTN_NONE)
			KOSM A 0 A_NoGravity
			KOSM ABCDE 4
			TNT1 A 1 
			Stop
	}
}

// Smoke trail
ACTOR SmokeTrail : BulletPuff
{
	+ALWAYSPUFF
	+CLIENTSIDEONLY
	+BLOODLESSIMPACT
	+NOPAIN
	VSpeed 0
	Scale 0.5
	RenderStyle Add
	Alpha 0.5
	Translation "168:191=80:106"
	States
	{
		Spawn:
		BAL1 c 0
		BAL1 CDE 4 A_FadeTo(0, 0.1)
		Stop
	}
}

// Daisy :)
ACTOR Daisy
{
	Monster
	Health 20000
	Radius 20
	painchance 256
	Height 56
	Mass 400
	speed 20
	+Solid
	+Shootable
	+FLOORCLIP
	+NOBLOOD
	+LOOKALLAROUND
	+QUICKTORETALIATE
	Obituary "%o should've run away."
	States
	{
		Spawn:
			DASY A 1 A_Look
			Loop
		See:
			DASY AABBCCDD 2 A_Chase
			Loop
		Pain:
			DASY AAA 0 A_SpawnItemEx("Heart", random(-12, 12), random(-12, 12), random(-12, 12))
			DASY A 1 ACS_ExecuteAlways(10, 0)
			Goto See
		Death:
			TNT1 A 1
			Stop
	}
}

// Daisy :) (Flight Wacky Mod)
ACTOR Daisy_Flight : Daisy
{
	+FLOAT
	+NOGRAVITY
}

// Daisy :) (Rage Wacky Mod)
ACTOR Daisy_Rage : Daisy
{
	// No changes
}

// Daisy :) (Spread Wacky Mod)
ACTOR Daisy_Spread : Daisy
{
	// No changes
}

// Petting Hands Weapon
ACTOR PetWeapon : Fist
{
	+FORCEPAIN
	+NOAUTOAIM
	States
	{
		Ready:
			TNT1 A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 1 A_Lower
			Loop
		Select:
			TNT1 A 1 A_Raise
			Loop
		Fire:
			PETH A 1 Offset(135, 64)
			PETH A 1 Offset(99, 42)
			PETH B 1 Offset(53, 33)
			PETH C 1 Offset(20, 31)
			PETH D 1 Offset(0, 32)
			PETH D 0 Offset(0, 48)
			PETH D 0 Offset(0, 64)
			PETH D 2 A_FireBullets(0, 0, 1, 20, "MeleePuff", FBF_NOFLASH | FBF_NORANDOM, 64) // A_CustomPunch(20, true, CPF_NOTURN) // CPF_NOTURN Flag unsupported in Zandronum :/
			PETH D 1 Offset(3, 58)
			PETH D 1 Offset(6, 52)
			PETH C 1 Offset(20, 58)
			PETH C 1 Offset(40, 64) A_Refire
			Goto Ready
	}
}

// Heart particles
ACTOR Heart
{
	scale 2
	+NOGRAVITY
	States
	{
		Spawn:
			HERT A 1 BRIGHT
			Goto Death
		Death:
			HERT A 0 BRIGHT ThrustThingZ(0, 16, 0, 0)
			HERT AAAAAAAAAA 2 BRIGHT A_FadeTo(0, 0.1)
			Stop
	}
}

// Fake Chainsaw
ACTOR ChainsawFake : Chainsaw
{
	States
	{
		Fire:
			Goto Ready
	}
}

// Fake Pistol
ACTOR PistolFake : Pistol
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	States
	{
		Fire:
			Goto Ready
	}
}

// Fake SuperShotgun
ACTOR SuperShotgunFake : SuperShotgun
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	States
	{
		Fire:
			Goto Ready
	}
}

// Fake Chaingun
ACTOR ChaingunFake : Chaingun
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	States
	{
		Fire:
			Goto Ready
	}
}

// Fake RocketLauncher
ACTOR RocketLauncherFake : RocketLauncher
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	States
	{
		Fire:
			Goto Ready
	}
}

// Fake PlasmaRifle
ACTOR PlasmaRifleFake : PlasmaRifle
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	States
	{
		Fire:
			Goto Ready
	}
}

// Fake BFG9000
ACTOR BFG9000Fake : BFG9000
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	States
	{
		Fire:
			Goto Ready
	}
}

// Statue of God
ACTOR BuuStatue 11158
{
	Radius 40
	Height 110
	scale 0.5
	+SOLID
	States
	{
		Spawn:
			STAT A -1
			Stop
	}
}

// Snow flake particle 1
ACTOR SnowFlake1
{
	Radius 8
	Height 1
	Mass 5
	RenderStyle TRANSLUCENT
	Alpha 0.8
	Scale 0.3
	+CLIENTSIDEONLY
	+NOGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	+INVULNERABLE
	+NONSHOOTABLE
	+CORPSE
	States
	{
		Spawn:
			SNOW A 1 BRIGHT
			SNOW A 8 BRIGHT A_Jump(160, "Wiggle")
			Loop
		Wiggle:          
			SNOW A 8 BRIGHT A_CStaffMissileSlither
			Goto Spawn
		Crash:
			SNOW A 16 BRIGHT
			Stop
	}
}

// Snow flake particle 2
ACTOR SnowFlake2 : SnowFlake1
{
	States
	{
		Spawn:
			SNOW B 1 BRIGHT
			SNOW B 8 BRIGHT A_Jump(160, "Wiggle")
			Loop
		Wiggle:          
			SNOW B 8 BRIGHT A_CStaffMissileSlither
			Goto Spawn
		Crash:
			SNOW B 16 BRIGHT
			Stop
	}
}

// Snow flake particle 3
ACTOR SnowFlake3 : SnowFlake1
{
	States
	{
		Spawn:
			SNOW C 1 BRIGHT
			SNOW C 8 BRIGHT A_Jump(160, "Wiggle")
			Loop
		Wiggle:          
			SNOW C 8 BRIGHT A_CStaffMissileSlither
			Goto Spawn
		Crash:
			SNOW C 16 BRIGHT
			Stop
	}
}


// Snow dropper
ACTOR SnowDropper 11159
{
	Radius 16
	Height 6
	RenderStyle NONE
	+NOGRAVITY
	+NOBLOCKMAP
	+NOTELEPORT
	+SPAWNCEILING
	+CEILINGHUGGER
	+NOCLIP
	States
	{
		Spawn:
			TNT1 A 0 A_Jump(256, "Spawn1", "Spawn2", "Spawn3")
		Spawn1:
			TNT1 A 10 A_SpawnItemEx("SnowFlake1", random(0, 128), 0, 0, 0, 0, -0.7, random(0, 360), 128)
			Goto Spawn
		Spawn2:
			TNT1 A 10 A_SpawnItemEx("SnowFlake2", random(0, 128), 0, 0, 0, 0, -0.7, random(0, 360), 128)
			Goto Spawn
		Spawn3:
			TNT1 A 10 A_SpawnItemEx("SnowFlake3", random(0, 128), 0, 0, 0, 0, -0.7, random(0, 360), 128)
			Goto Spawn
	}
}

// Turkey from Super Turbo Turkey Puncher 3 
ACTOR Turkey : DoomImp
{
	Health 80
	Radius 16
	Height 40
	Mass 10000
	Speed 0
	PainChance 255
	SeeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	Monster
	scale 0.5
	+FLOORCLIP
	States
	{
		Spawn:
			TURK A 10 A_Look
			Loop
		See:
			TURK A 3
			Loop
		Pain:
			TURK B 7 A_PlaySound("DoomWare/TurkeyPunch")
			Goto See
		Death:
			TURK B 0 A_PlaySound("DoomWare/TurkeyGib", CHAN_5)
			TURK B 0 A_GiveToTarget("WonRoundItem",1)
			TURK CDEFGHIJK 3
			TURK K 0 A_PlaySound("DoomWare/TurkeyEject", CHAN_6)
			TURK K 10
			TURK K 0 A_PlaySound("DoomWare/TurkeyGobble", CHAN_7)
			TURK K 0 A_SpawnItem("Turkey", 0, 128)
			// Intentional fallthrough
		FadeCorpse:
			TURK K 1 A_FadeOut
			Loop
		XDeath:
			TNT1 A 1
			Stop
	}
}

// Fast Fist Weapon from Super Turbo Turkey Puncher 3 
ACTOR FastFist : Fist
{
	+WEAPON.NOAUTOFIRE
	States
	{
		Ready:
			PUNG B 1 A_WeaponReady
			Loop
		Deselect:
			PUNG B 1 A_Lower
			Loop
		Select:
			PUNG B 1 A_Raise
			Loop
		Waiting:
			PUNG B 1
			PUNG B 0 A_ReFire("Waiting")
			Goto Ready
		Fire:
			PUNG B 1
			PUNG C 1
			PUNG D 2 A_FireBullets(0, 0, 1, 20, "MeleePuff", FBF_NOFLASH | FBF_NORANDOM, 64) // A_CustomPunch(20, true, CPF_NOTURN) // CPF_NOTURN Flag unsupported in Zandronum :/
			PUNG C 2
			PUNG B 0 A_ReFire("Waiting")
			Goto Ready
	}
}

// Corridor7 player morph
ACTOR C7Player : DoomPlayer
{
	+NOSKIN
	Player.ColorRange 112, 127
	Player.SoundClass "C7Man"
	Player.MorphWeapon "C7Gun"
	States
	{
		Spawn:
			C7PL A -1
			Loop
		See:
			C7PL ABCD 4
			Loop
		Melee:
			C7PL F 6 BRIGHT
			// Intentional fall-through
		Missile:
			C7PL E 6
			Goto Spawn
		Pain:
			C7PL G 4 
			C7PL G 4 A_Pain
			Goto Spawn
		Death:
			C7PL H 10
			C7PL I 10 A_PlayerScream
			C7PL J 10 A_NoBlocking
			C7PL KLM 10
			C7PL N -1
			Stop
		XDeath:
			Goto Death
	}
}

// Corridor 7 Weapon
ACTOR C7Gun : Chaingun
{
	Weapon.AmmoUse 0
	Weapon.BobSpeed 3
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.3
	Obituary "%o was mowed down by %k's C. A. W."
	States
	{
		Ready:
			C7GN A 1 A_WeaponReady
			Loop
		Deselect:
			C7GN A 1 A_Lower
			Loop
		Select:
			C7GN A 1 A_Raise
			Loop
		Fire:
			C7GN B 0 A_PlaySound("DoomWare/C7Gun", CHAN_WEAPON)
			C7GN B 4 A_FireBullets(0, 0, 1, 1, "BulletPuff", FBF_NOFLASH | FBF_NORANDOM, 64)
			C7GN C 0 A_PlaySound("DoomWare/C7Gun", CHAN_WEAPON)
			C7GN C 3 A_FireBullets(0, 0, 1, 1, "BulletPuff", FBF_NOFLASH | FBF_NORANDOM, 64)
			C7GN C 0 A_ReFire
			Goto Ready
	}
}

// Corridor 7 plant obstacle
ACTOR C7Plant 11160
{
	Radius 32
	height 64
	+SOLID
	States
	{
		Spawn:
			C7PT A 1
			Loop
	}
}

// Corridor 7 table obstacle
ACTOR C7Table 11161
{
	Radius 32
	height 32
	+SOLID
	States
	{
		Spawn:
			C7CH A 1
			Loop
	}
}

// Corridor 7 light
ACTOR C7Light 11162
{
	Radius 8
	height 8
	-SOLID
	+SPAWNCEILING
	+NOGRAVITY
	States
	{
		Spawn:
			C7LI ABCDEFGH 1 Bright
			Loop
	}
}

// Void Candlestick
ACTOR CandlestickVoid : Candlestick 11163
{
	+SPAWNCEILING
	+CEILINGHUGGER
	+NOGRAVITY
	States
	{
		Spawn:
			CNDU A -1 Bright
			Stop
	}
}

// Void Blue Torch 	
ACTOR BlueTorchVoid : BlueTorch 11164
{
	+SPAWNCEILING
	+CEILINGHUGGER
	+NOGRAVITY
	States
	{
		Spawn:
			TBUU ABCD 4 Bright
			Loop
	}
}

// Upside down Imp
ACTOR DoomImpVoid : DoomImp
{
	+CEILINGHUGGER
	+NOGRAVITY
	+SPAWNCEILING
	States
	{
		Spawn:
			TROU AB 10 A_Look
			Loop
		See:
			TROU AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			TROU EF 8 A_FaceTarget
			TROU G 6 A_TroopAttack
			Goto See
	}
}

// Upside down Imp (Flight Wacky Mod)
ACTOR DoomImpVoid_Flight : DoomImpVoid
{
	-CEILINGHUGGER
	+FLOAT
}

// Upside down Imp (Rage Wacky Mod)
ACTOR DoomImpVoid_Rage : DoomImpVoid
{
	States
	{
		Melee:
		Missile:
			TROU EF 4 A_FaceTarget
			TROU G 3 A_TroopAttack
			Goto See
	}
}

// Upside down Imp (Spread Wacky Mod)
ACTOR DoomImpVoid_Spread : DoomImpVoid
{
	States
	{
		Melee:
		Missile:
			TROU EF 8 A_FaceTarget
			TROO G 0 A_CustomMissile("DoomImpBall", 32, 0, -10)
			TROO G 0 A_CustomMissile("DoomImpBall", 32, 0, 10)
			TROU G 6 A_TroopAttack
			Goto See
	}
}

// Shorter explosive barrel for Bit Trip level
ACTOR ExplosiveBarrelShorter : ExplosiveBarrel 11165
{
	Height 16
}

// Spider-Man player morph
ACTOR SpiderManPlayer : DoomPlayer
{
	scale 0.22
	+NOSKIN
	Player.ColorRange 112, 127
	Player.SoundClass "SpiderMan"
	States
	{
		Spawn:
			SPMN A -1
			Loop
		See:
			SPMN ABCD 4
			Loop
		Death:
		XDeath:
			SPMN E 4
			SPMN F 4 A_PlayerScream
			SPMN G 4 A_NoBlocking
			SPMN H 4
			SPMN I -1
			Stop
	}
}

// Blue Balloon
ACTOR BlueBalloon : Ammo
{
	+NOGRAVITY
	vspeed 1.5
	Inventory.PickupMessage "Caught a blue balloon"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			BALL AABCDEEDCB 4
			Loop
		Death:
			TNT1 A 1
			stop
	}
}

// Blue Balloon but not flying
ACTOR BlueBalloonStill : Hat
{
	States
	{
		Spawn:
			BALL C 4
			Loop
	}
}

// Green Balloon
ACTOR GreenBalloon : Ammo
{
	Translation "196:203=115:123"
	+NOGRAVITY // Repeated code, because the ammo name wouldn't work via inheritance
	vspeed 1.5
	Inventory.PickupMessage "Caught a green balloon"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			BALL AABCDEEDCB 4
			Loop
		Death:
			TNT1 A 1
			stop
	}
}

// Green Balloon but not flying
ACTOR GreenBalloonStill : BlueBalloonStill
{
	Translation "196:203=115:123"
}

// Red Balloon
ACTOR RedBalloon : Ammo
{
	Translation "196:203=173:180"
	+NOGRAVITY
	vspeed 1.5
	Inventory.PickupMessage "Caught a red balloon"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			BALL AABCDEEDCB 4
			Loop
		Death:
			TNT1 A 1
			stop
	}
}

// Red Balloon but not flying
ACTOR RedBalloonStill : BlueBalloonStill
{
	Translation "196:203=173:180"
}

// Yellow Balloon
ACTOR YellowBalloon : Ammo
{
	Translation "196:203=160:166"
	+NOGRAVITY
	vspeed 1.5
	Inventory.PickupMessage "Caught a yellow balloon"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			BALL AABCDEEDCB 4
			Loop
		Death:
			TNT1 A 1
			stop
	}
}

// Yellow Balloon but not flying
ACTOR YellowBalloonStill : BlueBalloonStill
{
	Translation "196:203=160:166"
}

// Fred Chexter player morph
ACTOR FredPlayer : DoomPlayer
{
	+NOSKIN
	Player.MorphWeapon "Zorcher"
	Player.ColorRange 112, 127
	Player.SoundClass "FredChexter"
	States
	{
		Spawn:
			FRED A -1
			Loop
		See:
			FRED ABCD 4
			Loop
		Melee:
			FRED F 6 BRIGHT
			// Intentional fall-through
		Missile:
			FRED E 6
			Goto Spawn
	}
}

// Zorcher weapon
ACTOR Zorcher : Pistol
{
	Obituary "%o was sent to another dimension by %k."
	Inventory.Pickupmessage "Picked up a Zorcher."
	States
	{
		Ready:
			ZRCH A 1 A_WeaponReady
			Loop
		Deselect:
			ZRCH A 1 A_Lower
			Loop
		Select:
			ZRCH A 1 A_Raise
			Loop
		Fire:
			ZRCH A 4
			ZRCH B 0 A_FireBullets (5.6, 0, 1, 5, "ChexPuff")
			ZRCH B 0 A_PlaySound("DoomWare/Zorcher", CHAN_WEAPON)
			ZRCH B 6 A_GunFlash
			ZRCH C 4
			ZRCH B 5 A_ReFire
			Goto Ready
		Flash:
			ZRCH F 7 Bright A_Light1
			Goto LightDone
			ZRCH F 7 Bright A_Light1
			Goto LightDone
		Spawn:
			ZRCH A -1
			Stop
	}
}

// Zorcher bullet puff
ACTOR ChexPuff : BulletPuff
{
	+NODECAL
	States
	{
		Spawn:
			CQPF A 4 Bright
			CQPF B 4
			// Intentional fall-through
		Melee:
			CQPF CD 4
			Stop
	}
}

// Flemoid monster
ACTOR Flemoid : ZombieMan
{
	+NOBLOODDECALS
	+FLOORCLIP
	SeeSound "DoomWare/FlemoidSight"
	PainSound "DoomWare/FlemoidPain"
	DeathSound "DoomWare/FlemoidDie"
	ActiveSound "DoomWare/FlemoidActive"
	Obituary "%o was slimed by a flemoid"
	DropItem ""
	MinMissileChance 0
	Painchance 255
	BloodType ""
	States
	{
		Spawn:
			FLEM AB 10 A_Look
			Loop
		See:
			FLEM AABBCCDD 4 A_Chase
			Loop
		Missile:
			Goto See
		Pain:
			FLEM E 3
			FLEM E 3 A_Pain
			Goto See
		XDeath:
		Death:
			FLEM F 5 A_GiveToTarget("WonRoundItem",1)
			FLEM G 5 A_Scream
			FLEM H 5 A_NoBlocking
			FLEM I 5
			TNT1 A -1 
			Stop
	}
}

// Flemoid (Flight Wacky Mod)
ACTOR Flemoid_Flight : Flemoid
{
	+FLOAT
	+NOGRAVITY
}

// Flemoid (Rage Wacky Mod)
ACTOR Flemoid_Rage : Flemoid
{
	// No changes
}

// Flemoid (Spread Wacky Mod)
ACTOR Flemoid_Spread : Flemoid
{
	// No changes
}

// Chex Quest Floor Lamp
ACTOR ChexLamp : TechLamp2 11166
{
	States
	{
		Spawn:
			CQLT A 4 Bright
			Loop
	}
}

// Chex Quest Space Ship
ACTOR ChexShip : BigTree 11167
{
	States
	{
		Spawn:
			CQSH A -1
			Stop
	}
}

// Chex Quest Flag
ACTOR ChexFlag : TechLamp 11168
{
	States
	{
		Spawn:
			CQFL A -1
			Stop
	}
}

// Chex Quest Canister
ACTOR ChexCanister : BurningBarrel 11169
{
	States
	{
		Spawn:
			CQCN A -1
			Stop
	}
}

// Meteor
ACTOR Meteor
{
	projectile
	radius 16
	height 32
	scale 0.2
	+SPAWNCEILING
	+NOGRAVITY
	vspeed -7
	SeeSound "DoomWare/Meteor"
	States
	{
		Spawn:
			MTOR AAAAAAA 3 Bright A_SpawnItemEx("MeteorTrail")
			MTOR A 1 Bright A_PlaySound("DoomWare/Meteor", CHAN_AUTO, 0.8, false, ATTN_NONE)
		See:
			MTOR A 3 Bright A_SpawnItemEx("MeteorTrail")
			Loop
		Death:
			TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FloorProtrude",0,0,random(0,360),2,random(-20,90))
			BBOM A 0 A_Explode(512, 24)
			BBOM ABCDEFGHIJKLMNOPQRSTUVWX 1
			TNT1 A 1
			Stop
	}
}

// Meteor Trail
ACTOR MeteorTrail
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	scale 0.2
	States
	{
		Spawn:
			MTOR A 1 Bright A_SetScale(0.2)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.18)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.16)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.14)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.12)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.10)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.08)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.06)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.04)
			MTOR A 1 Bright A_FadeOut(0.1)
			MTOR A 1 Bright A_SetScale(0.02)
			MTOR A 1 Bright A_FadeOut(0.1)
			stop
	}
}

// F.E.A.R. Replica Soldier player morph
ACTOR ReplicaPlayer : DoomPlayer
{
	+PICKUP
	+NOSKIN
	BloodType "FearBlood"
	Player.ForwardMove 0.2
	Player.SideMove 0.2
	Player.SoundClass "Replica"
	Player.JumpZ 2
	Player.MorphWeapon "FearSPAS12"
	Player.ColorRange 112, 127
	gravity 0.05
	States
	{
		Spawn:
			REPL A -1
			Loop
		See:
			REPL ABCD 8 
			Loop
		Missile:
			REPL E 24
			Goto Spawn
		Melee:
			REPL F 12 BRIGHT
			Goto Missile
		Pain:
			REPL G 8 
			REPL G 8 A_Pain
			Goto Spawn
		Death:
			REPL H 15 A_PlayerScream
			REPL I 15 
			REPL J 15 A_NoBlocking
			REPL KLM 15
			REPL N -1
			Stop
		XDeath:
			REPL OOOOOOOOOOOOOOOOOOOO 0 A_SpawnItemEx("FearBlood", random(-5, 5), random(-6, 6), random(16, 48))
			REPL OOOOOOOOOOOOOOOOOOOO 0 A_SpawnItemEx("FearBloodCloud", random(-5, 5), random(-6, 6), random(16, 48))
			REPL O 15 A_XScream
			REPL P 15 
			REPL Q 15 A_NoBlocking
			REPL RSTU 15
			REPL V -1
			Stop
	}
}

// F.E.A.R. SPAS-12
ACTOR FearSPAS12 : Weapon
{
    Weapon.AmmoType "Shell"
    Weapon.AmmoGive 50
	Weapon.AmmoUse 1
    Weapon.KickBack 500
	Weapon.BobSpeed 0.5
	Weapon.BobRangeX 0.7
	Weapon.BobRangeY 0.7
	Obituary "%o was turned into mist by %k."
    States
    {
		Ready:
			SPAS A 1 A_WeaponReady
			Loop
		Deselect:
			SPAS A 1 A_Lower
			Loop
		Select:
			SPAS A 1 A_Raise
			Loop
		Fire:
			SPAS A 0 A_GunFlash
			SPAS A 0 A_PlaySound("DoomWare/SPAS12Fire")
			SPAS DD 0 BRIGHT A_SpawnItemEx("FearBulletSmoke", 20, 6, 32, 0, 4)
			SPAS D 0 BRIGHT A_FireCustomMissile("FearBulletGib", 0, 0)
			SPAS DDDDDDDD 0 BRIGHT A_FireCustomMissile("FearBullet", random(4,-4), 0, 0, random(4,-4))
			SPAS A 2 Offset(16, 40)
			SPAS A 2 Offset(32, 56)
			SPAS A 2 Offset(24, 49)
			SPAS A 2 Offset(12, 44)
			SPAS A 2 Offset(8, 40)
			SPAS A 4 Offset(4, 36)
			SPAS A 4 Offset(3, 35)
			SPAS A 4 Offset(2, 34)
			SPAS A 4 Offset(1, 33)
			SPAS A 4 Offset(0, 32)
		Pump:
			SPAS A 0 A_PlaySound("DoomWare/SPAS12Pump", CHAN_6)
			SPAS BCC 4
			SPAS C 0 A_SpawnItemEx("FearShellCasing", 20, -20, 28, 4, 0, 0, -90)
			SPAS CB 4
			SPAS A 4 Offset(2, 33)
			SPAS A 4 Offset(2, 31)
			SPAS A 4 Offset(1, 31)
			SPAS A 4 Offset(0, 32)
			goto Ready
	}
}

// F.E.A.R. slowmo bullet
ACTOR FearBullet
{
	Radius 13
	Height 8
	Projectile
	+BLOODSPLATTER
	Damage 8
	Speed 15
	Seesound ""
	Deathsound ""
	Scale 0.5
	RenderStyle Add
	Obituary "%o was turned into mist by %k."
	States
	{
		Spawn:
			PUFF B 0 ACS_NamedExecuteAlways("DoomWare_Entity_MarkCleanup", 0)
			PUFF B 1 BRIGHT A_CustomMissile("FearBulletTrail", 0)
			PUFF B 0 ACS_NamedExecuteAlways("DoomWare_Entity_CheckCleanup", 0)
			Loop
		Death:
			PUFF BBBBBBBBBB 0 A_CustomMissile("FearBulletSmoke", random(-10, 5), random(-8, 8))
			PUFF BB 0 A_CustomMissile("FearBulletDebris", random(-10, 5), random(-8, 8))
			PUFF BCD 8
			Stop
		}
}

// F.E.A.R. Shotgun Shell Casing
ACTOR FearShellCasing
{
	Height 8
	Radius 6
	Speed 4
	Scale 0.2
	BounceFactor 0.8
	Mass 1
	gravity 0.25
	RenderStyle Translucent
	Alpha 1.0
	PROJECTILE
	+CLIENTSIDEONLY
	+DOOMBOUNCE
	-NOGRAVITY  
	-NOBLOCKMAP
	SeeSound "DoomWare/ShellBounce"
	States
	{
		Spawn:
			SSHL ABCDEFGH 4
			Goto Spawn+1
		Death:
			SSHL H 40
		Fade:
			SSHL H 4 A_FadeOut(0.1)
			Loop
	}
}

// F.E.A.R. slowmo bullet
ACTOR FearBulletGib : FearBullet
{
	Radius 26
	Height 16
	-BLOODSPLATTER
	Damage 1337
	Speed 17
	States
	{
		Spawn:
			TNT1 A 17 
			Stop
		Death:
			Stop
		}
}

// F.E.A.R. slowmo bullet trail
ACTOR FearBulletTrail
{
	+NOCLIP
	+CLIENTSIDEONLY
	projectile
	Speed 0
	Alpha 0.5
	Scale 0.5
	Renderstyle Translucent
	States
	{
		Spawn:
			PUFF C 1 A_FadeOut(0.02)
			Loop
	}
}

// F.E.A.R. slowmo bullet smoke
ACTOR FearBulletSmoke
{
	projectile
	radius 5
	height 5
	alpha 0.5
	vspeed 1
	speed 1
	scale 0.25
	RenderStyle Add
	+CLIENTSIDEONLY
	+NOGRAVITY
	+DONTFALL
	States
	{
		Spawn:
			EFSM A 0
			EFSM A 1 A_ChangeVelocity(random(-1, 1), random(-1, 1), random(-1, 1), CVF_REPLACE)
			EFSM A 0 A_SetScale(0.25)
			EFSM A random(1, 10	) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.3)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.35)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.4)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.45)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.5)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.55)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.6)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.65)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.7)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.75)
			EFSM A random(1, 10) A_FadeOut(0.05)
			Stop
		Death:
			Stop
	}
}

// F.E.A.R. slowmo bullet spark
ACTOR FearBulletSpark : FearBulletSmoke
{
	alpha 1
	scale 0.5
	-NOGRAVITY
	gravity 0.1
	States
	{
		Spawn:
			PUFF B 0
			PUFF B 1 A_ChangeVelocity(random(-2, 2), random(-2, 2), random(2, 7), CVF_REPLACE)
		Fade:
			PUFF B 1 A_FadeOut(frandom(0.01, 0.05))
			Loop
	}
}

// F.E.A.R. slowmo bullet debris
ACTOR FearBulletDebris : FearBulletSmoke
{
	alpha 1
	scale 0.5
	-NOGRAVITY
	gravity 0.1
	States
	{
		Spawn:
			EFDB A 0
			EFDB A 0 A_SetScale(frandom(0.25, 0.5))
			EFDB A 1 A_ChangeVelocity(random(-1, 1), random(-1, 1), random(0, 4), CVF_REPLACE)
		Fade:
			EFDB A random(1, 5) A_FadeOut(frandom(0.01, 0.05))
			Loop
	}
}

// F.E.A.R. slowmo blood
ACTOR FearBlood : FearBulletSpark
{
	scale 1
	Alpha 1
	Renderstyle Normal
	States
	{
		Spawn:
			BLUD C 0
			BLUD C 1 A_ChangeVelocity(random(-2, 2), random(-2, 2), random(2, 4), CVF_REPLACE)
		Fade:
			BLUD C 0 A_CustomMissile("FearBloodTrail", 0)
			BLUD CB 5 A_FadeOut(0.01)
			Loop
	}
}

// F.E.A.R. slowmo bullet trail
ACTOR FearBloodTrail : FearBulletTrail
{
	-NOCLIP
	Alpha 1
	Scale 1
	Renderstyle Normal
	States
	{
		Spawn:
			BLUD C 1 A_FadeOut(0.02)
			Loop
	}
}

// F.E.A.R. blood cloud
ACTOR FearBloodCloud : FearBulletSmoke
{
	RenderStyle Normal
	States
	{
		Spawn:
			EFGC A 0
			EFGC A 1 A_ChangeVelocity(random(-2, 2), random(-2, 2), random(-2, 2), CVF_REPLACE)
			EFGC A 0 A_SetScale(0.5)
			EFGC A random(1, 10	) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.55)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.6)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.65)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.7)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.75)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.8)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.85)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.9)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(0.95)
			EFGC A random(1, 10) A_FadeOut(0.05)
			EFGC A 0 A_SetScale(1.0)
			EFGC A random(1, 10) A_FadeOut(0.05)
			Stop
		Death:
			Stop
	}
}

// F.E.A.R. fire extinguisher
ACTOR FearExtinguisher : ExplosiveBarrel 11170
{
	radius 5
	height 22
	+NOGRAVITY
	+DONTFALL
	Obituary "%o stood too close to a fire extinguisher."
	States
	{
		Spawn:
			FEXT A 5
			Loop
		Death:
			FEXT AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FearExtinguisherSmoke", random(-10, 5), random(-8, 8))
			FEXT A 0 A_PlaySound("DoomWare/ExtinguisherExplode")
			FEXT A 0 A_Explode
			TNT1 A 3 
			FEXT AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FearExtinguisherSmoke", random(-10, 5), random(-8, 8))
			Stop
	}
}

// F.E.A.R. fire extinguisher explosion smoke
ACTOR FearExtinguisherSmoke : FearBulletSmoke
{
	States
	{
		Spawn:
			EFSE A 0
			EFSE A 1 A_ChangeVelocity(random(-2, 2), random(-2, 2), random(-2, 2), CVF_REPLACE)
			EFSE A 0 A_SetScale(0.5)
			EFSE A random(1, 10	) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.55)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.6)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.65)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.7)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.75)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.8)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.85)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.9)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.95)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(1.0)
			EFSE A random(1, 10) A_FadeOut(0.05)
			Stop
		Death:
			Stop
	}
}

// F.E.A.R. Glass Shard Particle
ACTOR FearGlassShard : GlassShard
{
	Radius 2
	Height 4
	Scale 0.5
	Alpha 0.5
	Gravity 0.1
	RenderStyle Translucent
	-NOGRAVITY
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 A 0 ThrustThing(angle*256/360+random(-10, 10), random(1, 7), 0, 0)
			TNT1 A 0 ThrustThingZ(0, random(0, 3), random(0, 1), 0)
			TNT1 A 0 A_Jump(256, "Idle1", "Idle2", "Idle3", "Idle4")
		Idle1:
			SHRD A 4
			Loop
		Idle2:
			SHRD B 4
			Loop
		Idle3:
			SHRD C 4
			Loop
		Idle4:
			SHRD D 4
			Loop
		Death:
			SHRD A 30 A_Jump(200, "Death")
			Stop
	}
}

// F.E.A.R. office chair
ACTOR FearOfficeChair 11171
{
	Radius 16
	Height 32
	ProjectilePassHeight -16
	+SOLID
	States
	{
		Spawn:
			OFCH A 4
			Loop
	}
}

// F.E.A.R. office plant
ACTOR FearOfficePlant 11172
{
	Radius 8
	Height 16
	+SOLID
	States
	{
		Spawn:
			OFPL A 4
			Loop
	}
}

// F.E.A.R. Ceiling Lamp
ACTOR CeilingLamp 11173
{
	Radius 11
	height 32
	-SOLID
	+SPAWNCEILING
	+NOGRAVITY
	States
	{
		Spawn:
			CLMP A 1 Bright
			Loop
	}
}

// Mr. Shifty player morph
ACTOR ShiftyPlayer : DoomPlayer
{
	+PICKUP
	+NOSKIN
	Player.SoundClass "ShiftyPlayer"
	Player.MorphWeapon "ShiftyFist"
	Player.ColorRange 112, 127
	scale 0.4
	States
	{
		Spawn:
			SHFT A -1
			Loop
		See:
			SHFT ABCD 4 
			Loop
		Missile:
		Melee:
			SHFT EEFFF 3 BRIGHT
			Goto Spawn
		Pain:
			Goto Spawn
		Death:
		XDeath:
			SHFT G 4 A_PlayerScream
			SHFT H 4 
			SHFT I 4 A_NoBlocking
			SHFT J 4
			SHFT K -1
			Stop
	}
}

// Mr Shifty teleport ammo
actor ShiftyAmmo : Ammo
{
	Inventory.Amount 5
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 5
	Ammo.BackpackMaxAmount 5
}

// Mr Shifty fist weapon
ACTOR ShiftyFist : Fist
{
	//+WEAPON.NOAUTOFIRE // Sadly doesn't work with A_ReFire...
	Weapon.AmmoType ShiftyAmmo
	Weapon.AmmoGive 5
	Weapon.AmmoUse 0
	Obituary "%o was sent flying by %k"
	States
	{
		Fire:
			SFST A 0 A_Recoil(-5)
			SFST A 2 Offset(200, 116) A_PlaySound("DoomWare/ShiftyFist1")
			SFST A 2 Offset(150, 95)
			SFST A 1 Offset(100, 74)
			SFST A 1 Offset(50, 53)
			SFST A 1 Offset(1, 20) A_FireBullets(0, 0, 1, 1337, "ShiftyPuff", FBF_NOFLASH | FBF_NORANDOM, 64)
			SFST A 1 Offset(2, 20)
			SFST A 1 Offset(3, 20)
			SFST A 1 Offset(4, 20)
			SFST A 3 A_ReFire("Fire2")
			SFST A 1 Offset(10, 53)
			SFST A 1 Offset(20, 74)
			SFST A 1 Offset(30, 95)
			SFST A 1 Offset(40, 116)
			SFST A 0 Offset(0, 32)
			Goto Ready
		Fire2:
			SFST A 0 A_Recoil(-5)
			SFST B 2 Offset(-200, 116) A_PlaySound("DoomWare/ShiftyFist2")
			SFST B 2 Offset(-150, 95)
			SFST B 1 Offset(-100, 74)
			SFST B 1 Offset(-50, 53)
			SFST B 1 Offset(-1, 20) A_FireBullets(0, 0, 1, 1337, "ShiftyPuff", FBF_NOFLASH | FBF_NORANDOM, 64)
			SFST B 1 Offset(-2, 20)
			SFST B 1 Offset(-3, 20)
			SFST B 1 Offset(-4, 20)
			SFST B 3 A_ReFire("Fire")
			SFST B 1 Offset(-10, 53)
			SFST B 1 Offset(-20, 74)
			SFST B 1 Offset(-30, 95)
			SFST B 1 Offset(-40, 116)
			SFST B 0 Offset(0, 32)
			Goto Ready
	}
}

// Mr Shifty fist bullet puff
ACTOR ShiftyPuff : MeleePuff
{
	+PUFFONACTORS
	AttackSound "DoomWare/ShiftyFistHit"
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Quake(1, 35, 0, 64, "")
			TNT1 A 5
			Stop
	}
}

// Mr Shifty teleport effect
Actor ShiftyEffect : PlasmaBall
{
	Projectile
	radius 5
	height 5
	mass 0
	speed 0
	damage 0
	-SOLID
	+NOGRAVITY
	+DONTFALL
	+CLIENTSIDEONLY
	States
	{
		Spawn:
		Death:
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ShiftySmoke", random(-8, 8), random(-8, 8), random(5, 20))
			TNT1 A 1
			Stop
	}
}

// Mr Shifty teleport effect 2
Actor ShiftyEffect2
{
	radius 64
	height 64
	mass 0
	alpha 1
	scale 0.5
	RenderStyle Translucent
	-SOLID
	+NOGRAVITY
	+DONTFALL
	+CLIENTSIDEONLY
	States
	{
		Spawn:
		Death:
			SSMK A 1
			SSMK A 0 A_SetScale(0.5, 0.5)
			SSMK A 1 A_FadeOut(0.1)
			SSMK A 0 A_SetScale(0.4, 0.5)
			SSMK A 1 A_FadeOut(0.1)
			SSMK A 0 A_SetScale(0.3, 0.5)
			SSMK A 1 A_FadeOut(0.1)
			SSMK A 0 A_SetScale(0.2, 0.5)
			SSMK A 1 A_FadeOut(0.1)
			SSMK A 0 A_SetScale(0.1, 0.5)
			SSMK A 1 A_FadeOut(0.1)
			Stop
	}
}

// Mr Shifty smoke
ACTOR ShiftySmoke : FearBulletSmoke
{
	RenderStyle Translucent
	States
	{
		Spawn:
			EFSB A 0
			EFSB A 1 A_ChangeVelocity(random(-2, 2), random(-2, 2), random(-2, 2), CVF_REPLACE)
			EFSB A 0 A_SetScale(0.5)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.45)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.4)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.35)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.3)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.25)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.2)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.15)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.1)
			EFSB A random(1, 3) A_FadeOut(0.05)
			EFSB A 0 A_SetScale(0.05)
			EFSB A random(1, 3) A_FadeOut(0.05)
			Stop
		Death:
			Stop
	}
}

// Batman player morph
ACTOR BatmanPlayer : DoomPlayer
{
	+PICKUP
	+NOSKIN
	Player.SoundClass "BatmanPlayer"
	Player.MorphWeapon "Batarang"
	Player.ColorRange 112, 127
	Scale 0.8
	States
	{
		Spawn:
			BATM A -1
			Loop
		See:
			BATM ABCD 4 
			Loop
		Missile:
		Melee:
		Pain:
			Goto Spawn
		Death:
		XDeath:
			BATM E 5
			BATM F 5 A_PlayerScream
			BATM G 5 A_NoBlocking
			BATM HIJ 5
			BATM K -1
			Stop
	}
}

// Batman Batarang
ACTOR Batarang : DoomWeapon
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 1
	Weapon.AmmoType "Clip"
	States
	{
		Ready:
			BATR A 1 A_WeaponReady
			Loop
		Deselect:
			BATR A 1 A_Lower
			Loop
		Select:
			BATR A 1 A_Raise
			Loop
		Fire:
			Goto Ready
		Spawn:
			BATR A -1
			Stop
	}
}

// Batman Criminal (Melee)
ACTOR CriminalMelee : Demon
{
	+INVULNERABLE
	+LOOKALLAROUND
	painchance 0
	SeeSound "DoomWare/Criminal1See"
	AttackSound ""
	MeleeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	Scale 0.8
	Obituary "%o was shanked."
	MeleeDamage 150
    MeleeRange 64
	States
	{
		Spawn:
			CRM1 AB 10 A_Look
			Loop
		See:
			CRM1 AABBCCDD 2 Fast A_Chase
			Loop
		Melee:
			CRM1 E 0 A_PlaySound("DoomWare/Criminal1Attack")
			CRM1 EF 8 Fast A_FaceTarget
			CRM1 G 8 Fast A_MeleeAttack
			Goto See
	}
}

// Batman Criminal (Melee) (Flight Wacky Mod)
ACTOR CriminalMelee_Flight : CriminalMelee
{
	+FLOAT
	+NOGRAVITY
}

// Batman Criminal (Melee) (Rage Wacky Mod)
ACTOR CriminalMelee_Rage : CriminalMelee
{
	States
	{
		Melee:
			CRM1 E 0 A_PlaySound("DoomWare/Criminal1Attack")
			CRM1 EF 4 Fast A_FaceTarget
			CRM1 G 4 Fast A_MeleeAttack
			Goto See
	}
}

// Batman Criminal (Melee) (Spread Wacky Mod)
ACTOR CriminalMelee_Spread : CriminalMelee
{
	// No changes
}

// Batman Criminal (Ranged)
ACTOR CriminalRanged : DoomImp
{
	+INVULNERABLE
	+LOOKALLAROUND
	painchance 0
	SeeSound "DoomWare/Criminal2See"
	AttackSound ""
	MeleeSound ""
	PainSound ""
	DeathSound ""
	ActiveSound ""
	MeleeDamage 150
	Scale 0.8
	Obituary "%o did not see that shuriken."
	States
	{
		Spawn:
			CRM2 AB 10 A_Look
			Loop
		See:
			CRM2 AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			CRM2 EF 8 A_FaceTarget
			CRM2 G 6 A_CustomComboAttack("CriminalShuriken", 32, 150, "DoomWare/Criminal2Melee")
			Goto See
	}
}

// Batman Criminal (Ranged) (Flight Wacky Mod)
ACTOR CriminalRanged_Flight : CriminalRanged
{
	+FLOAT
	+NOGRAVITY
}

// Batman Criminal (Ranged) (Rage Wacky Mod)
ACTOR CriminalRanged_Rage : CriminalRanged
{
	States
	{
		Melee:
		Missile:
			CRM2 EF 4 A_FaceTarget
			CRM2 G 3 A_CustomComboAttack("CriminalShuriken", 32, 150, "DoomWare/Criminal2Melee")
			Goto See
	}
}

// Batman Criminal (Ranged) (Spread Wacky Mod)
ACTOR CriminalRanged_Spread : CriminalRanged
{
	States
	{
		Melee:
		Missile:
			CRM2 EF 8 A_FaceTarget
			TROO G 0 A_CustomMissile("CriminalShuriken", 32, 0, -10)
			TROO G 0 A_CustomMissile("CriminalShuriken", 32, 0, 10)
			CRM2 G 6 A_CustomComboAttack("CriminalShuriken", 32, 150, "DoomWare/Criminal2Melee")
			Goto See
	}
}

// Batman ranged criminal shuriken
ACTOR CriminalShuriken
{
	Radius 6
	Height 8
	Speed 10
	FastSpeed 20
	Damage 150
	Projectile
	SeeSound "DoomWare/Criminal2Attack"
	DeathSound "DoomWare/Criminal2Hit"
	Obituary "%o did not see that shuriken."
	States
	{
		Spawn:
			CRM2 HI 4 Bright
			Loop
		Death:
			CRM2 JKL 6 Bright
			Stop
	}
}

// Batman Statue
ACTOR BatmanStatue : BigTree 11176
{
	Radius 32
	Height 108
	States
	{
		Spawn:
			BATD D -1
			Stop
	}
}

// Batman Computer
ACTOR BatmanComputer : BigTree 11177
{
	Radius 8
	Height 32
	States
	{
		Spawn:
			BATD A -1
			Stop
	}
}

// Batman Table
ACTOR BatmanTable : BigTree 11178
{
	Radius 32
	Height 42
	States
	{
		Spawn:
			BATD B -1
			Stop
	}
}

// Batman Street Light
ACTOR BatmanStreetLight : BigTree 11179
{
	Radius 32
	Height 172
	States
	{
		Spawn:
			BATD C -1 BRIGHT
			Stop
	}
}

// Batman Chandelier
ACTOR BatmanChandelier : HangingCorpse 11180
{
	Radius 32
	Height 64
	States
	{
		Spawn:
			BATD E -1 BRIGHT
			Stop
	}
}

// Batman Lamp
ACTOR BatmanLamp : HangingCorpse 11181
{
	Radius 8
	Height 32
	-SOLID
	States
	{
		Spawn:
			BATD F -1 BRIGHT
			Stop
	}
}

// Batman Plant
ACTOR BatmanPlant : BigTree 11182
{
	Radius 10
	Height 24
	States
	{
		Spawn:
			BATD G -1
			Stop
	}
}

// On fire player morph
ACTOR FirePlayer : DoomPlayer
{
	Player.ForwardMove 0
	Player.SideMove 0
	+PICKUP
	+NOSKIN
	Scale 0.5
	States
	{
		Spawn:
			FLMN A 1 
			FLMN A 0 A_PlaySound("DoomWare/Engulf", CHAN_ITEM)
			FLMN A 0 A_PlaySound("DoomWare/OnFireScream", CHAN_VOICE)
		See:
			FLMN A 0 A_PlaySound("vile/firecrkl", CHAN_BODY)
			FLMN ABCDEF 3 A_Recoil(-3)
			FLMN A 0 A_SetAngle(angle+random(-40, 40))
			Loop
		Missile:
		Melee:
		Pain:
			Goto Spawn
		Death:
		XDeath:
			FLMN G 3
			FLMN H 3 A_PlaySound("DoomWare/OnFireDie", CHAN_VOICE)
			FLMN I 3 A_NoBlocking
			FLMN JKLMNOPQRS 3
			FLMN T -1
			Stop
	}
}

// Fire extinguish effect spawner
ACTOR ExtinguishEffect
{
	States
	{
		Spawn:
			TNT1 A 1
			TNT1 AAAAAAA 0 A_SpawnItemEx("ExtinguishSteam", random(-8, 8), random(-8, 8), 5)
			TNT1 A 1
			Stop
	}
}

// Fire extinguish steam
ACTOR ExtinguishSteam : FearBulletSmoke
{
	+NOGRAVITY
	-SOLID
	+NOCLIP
	States
	{
		Spawn:
			EFSE A 0
			EFSE A 1 A_ChangeVelocity(random(-2, 2), random(-2, 2), random(1, 3), CVF_REPLACE)
			EFSE A 0 A_SetScale(0.2)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.25)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.3)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.35)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.4)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.45)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.5)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.85)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.5)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.55)
			EFSE A random(1, 10) A_FadeOut(0.05)
			EFSE A 0 A_SetScale(0.6)
			EFSE A random(1, 10) A_FadeOut(0.05)
			Stop
		Death:
			Stop
	}
}

// Scientist 
ACTOR Scientist : BigTree 11183
{
	Radius 20
	Height 56
	States
	{
		Spawn:
			SCIE A -1
			Stop
	}
}

// Imp Body Part (Legs) 
ACTOR ImpBodyLegs
{
	States
	{
		Spawn:
		Parts1:
			IMLG A 1
			Loop
		Parts2:
			IMLG B 1
			Loop
		Parts3:
			IMLG C 1
			Loop
		Parts4:
			IMLG D 1
			Loop
		Parts5:
			IMLG E 1
			Loop
	}
}

// Imp Body Part (Chest) 
ACTOR ImpBodyChest
{
	States
	{
		Spawn:
		Parts1:
			IMBD A 1
			Loop
		Parts2:
			IMBD B 1
			Loop
		Parts3:
			IMBD C 1
			Loop
		Parts4:
			IMBD D 1
			Loop
		Parts5:
			IMBD E 1
			Loop
	}
}

// Imp Body Part (Head) 
ACTOR ImpBodyHead
{
	States
	{
		Spawn:
		Parts1:
			IMHD A 1
			Loop
		Parts2:
			IMHD B 1
			Loop
		Parts3:
			IMHD C 1
			Loop
		Parts4:
			IMHD D 1
			Loop
		Parts5:
			IMHD E 1
			Loop
	}
}

// Imp (Flight Wacky Mod)
actor DoomImp_Flight : DoomImp
{
	+NOGRAVITY
	+FLOAT
}

// Imp (Rage Wacky Mod)
actor DoomImp_Rage : DoomImp
{
	States
	{
		Missile:
			TROO EF 4 A_FaceTarget
			TROO G 3 A_TroopAttack
			Goto See
	}
}

// Imp (Spread Wacky Mod)
actor DoomImp_Spread : DoomImp
{
	States
	{
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 0 A_CustomMissile("DoomImpBall", 32, 0, -10)
			TROO G 0 A_CustomMissile("DoomImpBall", 32, 0, 10)
			TROO G 6 A_CustomComboAttack("DoomImpBall", 32, 3 * random(1, 8), "imp/melee")
			Goto See
	}
}

// Arch-Vile (Flight Wacky Mod)
actor Archvile_Flight : Archvile
{
	+FLOAT
	+NOGRAVITY
}

// Arch-Vile (Rage Wacky Mod)
actor Archvile_Rage : Archvile
{
	States
	{
		Missile:
			VILE G 0 Bright A_VileStart
			VILE G 5 Bright A_FaceTarget
			VILE H 4 Bright A_VileTarget
			VILE IJKLMN 4 Bright A_FaceTarget
			VILE O 4 Bright A_VileAttack
			VILE P 10 Bright
			Goto See
	}
}

// Arch-Vile (Spread Wacky Mod)
actor Archvile_Spread : Archvile
{
	// No changes
}

// Fish (Guppy)
ACTOR Guppy 11184
{
	Radius 8
	Height 16
	Speed 8
	Monster
	scale 0.25
	+FLOAT
	+NOGRAVITY
	+FRIGHTENED
	-SOLID
	States
	{
		Spawn:
		See:
			GUPY ABCB 3 A_Wander
			GUPY A 0 A_JumpIf(waterlevel != 3, "ThrustDown")
			Loop
		ThrustDown:
			GUPY A 0 ThrustThingZ(0, 8, 1, 1)
			Goto See
	}
}

// Fish (Carnivore)
ACTOR Carnivore : Guppy 11185
{
	Radius 16
	Height 32
	Speed 8
	scale 0.25
	States
	{
		Spawn:
		See:
			CARV ABCB 3 A_Wander
			CARV A 0 A_JumpIf(waterlevel != 3, "ThrustDown")
			Loop
	}
}

// Fish (Angie)
ACTOR Angie : Carnivore 11186
{
	States
	{
		Spawn:
		See:
			ANGI ABCB 3 A_Wander
			ANGI A 0 A_JumpIf(waterlevel != 3, "ThrustDown")
			Loop
	}
}

// Fish (Nostradamus)
ACTOR Nostradamus : Carnivore 11187
{
	States
	{
		Spawn:
		See:
			NOST ABCB 3 A_Wander
			NOST A 0 A_JumpIf(waterlevel != 3, "ThrustDown")
			Loop
	}
}

// Fish (Zorf)
ACTOR Zorf : Carnivore 11188
{
	States
	{
		Spawn:
		See:
			ZORF ABCB 3 A_Wander
			ZORF A 0 A_JumpIf(waterlevel != 3, "ThrustDown")
			Loop
	}
}

// Fish (Rufus)
ACTOR Rufus : Carnivore 11189
{
	-FLOAT
	-NOGRAVITY
	+FLOORCLIP
	States
	{
		Spawn:
		See:
			RUFS ABCB 3 A_Wander
			RUFS A 0 A_JumpIf(waterlevel != 3, "ThrustDown")
			Loop
	}
}

Actor PongBall
{
	Radius 8
	Height 16
	Speed 10
	Projectile
	+BOUNCEONWALLS
	+BOUNCEONACTORS
	+ALLOWBOUNCEONACTORS
	Scale 8
	BounceType "Hexen"
	BounceFactor 1.0
	WallBounceFactor 1.0
	BounceSound "DoomWare/PongBounce"
	States
	{
		Spawn:
			PONG A 4 Bright A_ScaleVelocity(1.03)
			Loop
		Death:
			TNT1 A 1 Bright
			Stop
	}
}

// Z-Fighter player morph
ACTOR ZFighterPlayer : DoomPlayer
{
	Player.ForwardMove 0
	Player.SideMove 0
	+PICKUP
	+NOSKIN
	States
	{
		Spawn:
			FSTR A 1
		See:
			FSTR A 0 A_Jump(256, "Punch", "Hurt", "Punch", "Hurt", "Punch", "Hurt", "Punch", "Hurt", "Walk1", "Walk2", "Walk3", "Walk4")
			FSTR A 1 
			Loop
		Punch:
			FSTR E 3
			Goto See
		Hurt:
			FSTR F 3
			Goto See
		Walk1:
			FSTR A 3
			Goto See
		Walk2:
			FSTR B 3
			Goto See
		Walk3:
			FSTR C 3
			Goto See
		Walk4:
			FSTR D 3
			Goto See
		Victory:
			FSTR G 3
			Loop
		Missile:
		Melee:
		Pain:
			Goto Spawn
		Death:
		XDeath:
			FSTR H 4
			FSTR I 4 A_Scream
			FSTR J 4 A_NoBlocking
			FSTR KLM 4
			FSTR N -1
			Stop
	}
}

// Brawler player morph
ACTOR BrawlerPlayer : DoomPlayer
{
	+PICKUP
	+NOSKIN
	Player.JumpZ 14
	Player.ForwardMove 0
	Player.MorphWeapon "BrawlerFist"
	Player.ColorRange 112, 127
	BloodType "BrawlerImpact"
	States
	{
		Spawn:
			BRAR A 0 A_JumpIfInventory("BrawlerLeft", 1, "SpawnLeft")
			BRAR A 4
			Loop
		SpawnLeft:
			BRAL A 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto Spawn
			BRAL A 4
			Loop
		See:
			BRAR A 0 A_JumpIfInventory("BrawlerLeft", 1, "SeeLeft")
			BRAR A 4
			BRAR B 0 A_JumpIfInventory("BrawlerLeft", 1, "SeeLeft")
			BRAR B 4 
			BRAR C 0 A_JumpIfInventory("BrawlerLeft", 1, "SeeLeft")
			BRAR C 4 
			BRAR D 0 A_JumpIfInventory("BrawlerLeft", 1, "SeeLeft")
			BRAR D 4 
			Loop
		SeeLeft:
			BRAL A 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto See
			BRAL A 4
			BRAL B 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto See
			BRAL B 4 
			BRAL C 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto See
			BRAL C 4 
			BRAL D 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto See
			BRAL D 4 
			Loop
		Missile:
		Melee:
			BRAR E 0 A_JumpIfInventory("BrawlerLeft", 1, "MeleeLeft")
			BRAR E 4
			BRAR E 0 A_JumpIfInventory("BrawlerLeft", 1, "MeleeLeft")
			BRAR E 4
			Goto Spawn
		MeleeLeft:
			BRAL E 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto Melee
			BRAL E 4
			BRAL E 0 A_JumpIfInventory("BrawlerLeft", 1, 1)
			Goto Melee
			BRAL E 4
			Goto SeeLeft
		Pain:
			Goto Spawn
		Death:
		XDeath:
			FSTR H 4 A_PlayerScream
			FSTR I 4 
			FSTR J 4 A_NoBlocking
			FSTR KLM 4
			FSTR N -1
			Stop
	}
}

// Brawler fist weapon
ACTOR BrawlerFist : Fist
{
	+WEAPON.NOAUTOFIRE
	Obituary "%o was decked by %k"
	States
	{
		Fire:
			
			SFST A 0 A_Recoil(-5)
			SFST A 1 Offset(200, 116) A_PlaySound("DoomWare/ShiftyFist1")
			SFST A 1 Offset(150, 95)
			SFST A 1 Offset(50, 53)
			SFST A 0 A_JumpIfInventory("BrawlerLeft", 1, "PunchLeft")
			SFST A 1 Offset(1, 20) A_FireCustomMissile("BrawlerHitbox", 90, false, -32)
			Goto CoolDown
		PunchLeft:
			SFST A 1 Offset(1, 20) A_FireCustomMissile("BrawlerHitbox", -90, false, 32)
			// Intentional fallthrough
		CoolDown:
			SFST A 1 Offset(2, 20)
			SFST A 1 Offset(4, 20)
			SFST A 2 
			SFST A 1 Offset(10, 53)
			SFST A 1 Offset(30, 95)
			SFST A 0 Offset(0, 32)
			Goto Ready
	}
}

// Item for handling Brawler looking left
ACTOR BrawlerLeft : Ammo 10469
{
	Inventory.PickupMessage ""
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	states
	{
		Spawn:
			RING ABCD 4
			Loop
		Death:
			RING A 0
			stop
	}
}

// Brawler Punch hitbox
ACTOR BrawlerHitbox
{
	Radius 16
	Height 8
	Speed 4
	Damage 169
	Projectile
	obituary "%o was decked by %k"
	+RIPPER
	States
	{
		Spawn:
			TNT1 A 4
			Stop
	}
}

// Brawler hit effect
ACTOR BrawlerImpact
{
	Mass 5
	+NOBLOCKMAP
	+NOTELEPORT
	+ALLOWPARTICLES
	+NOGRAVITY
	-SOLID
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 1 A_Quake(1, 35, 0, 1024, "")
			TNT1 A 0 A_PlaySound("DoomWare/ShiftyFistHit", CHAN_BODY, 1.0, 0, ATTN_NONE)
			TNT1 A 0 A_Jump(256, "Hit1", "Hit2", "Hit3", "Hit4")
		Hit1:
			BHTA ABCD 2 Bright
			Stop
		Hit2:
			BHTB ABCD 2 Bright
			Stop
		Hit3:
			BHTC ABCD 2 Bright
			Stop
		Hit4:
			BHTD ABCDE 2 Bright
			Stop
	}
}

// Landmine
ACTOR Landmine
{
	Monster
	Speed 0
	Radius 10
	Height 5
	scale 0.25
	+LOOKALLAROUND
	Obituary "%o did not see that landmine."
	States
	{
		Spawn:
			MINE A 2 A_LookEx(LOF_NOSOUNDCHECK | LOF_DONTCHASEGOAL, 0, 64)
			Loop
		See:
			BBOM A 0 A_SetScale(0.5, 0.5)
			BBOM A 0 A_Explode(169, 128)
			BBOM A 0 A_PlaySound("DoomWare/LandMine")
			BBOM AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("LandmineSmoke", random(-10, 5), random(-8, 8))
			BBOM ABCDEFGHIJKLMNOPQRSTUVWX 1 Bright
			TNT1 A 1
			Wait
	}
}

// Landmine sand smoke
ACTOR LandmineSmoke : FearBulletSmoke
{
	-NOGRAVITY
	Mass 1
	Gravity 0.3
	RenderStyle Translucent
	States
	{
		Spawn:
			EFSM A 0
			EFSM A 1 A_ChangeVelocity(random(-4, 4), random(-4, 4), random(4, 10), CVF_REPLACE)
			EFSM A 0 A_SetScale(0.5)
			EFSM A random(1, 10	) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.55)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.6)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.65)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.7)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.75)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.8)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.85)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.9)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(0.95)
			EFSM A random(1, 10) A_FadeOut(0.05)
			EFSM A 0 A_SetScale(1.0)
			EFSM A random(1, 10) A_FadeOut(0.05)
			Stop
		Death:
			Stop
	}
}

ACTOR PopupSpikes
{
	-SOLID
	Height 32
	Radius 16
	States
	{
		Spawn:
			TNT1 A 56
			STAB A 0 A_PlaySound("DoomWare/SpikesPopup")
			STAB AAAAAAAAAAAAAA 4 A_Explode(169, 16, 0, 0, 16)
			STAB A 0 A_PlaySound("DoomWare/SpikesPopup")
			Loop
	}
}